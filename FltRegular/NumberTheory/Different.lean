import Mathlib.RingTheory.DedekindDomain.Different
import Mathlib.RingTheory.DedekindDomain.Ideal
import Mathlib.RingTheory.Discriminant
import Mathlib.RingTheory.Localization.FractionRing
import FltRegular.NumberTheory.QuotientTrace
import Mathlib.NumberTheory.KummerDedekind
/-!
# The different ideal

## Main definition
- `traceFormDualSubmodule`: The dual `B`-submodule of `L` under the trace form.
- `FractionalIdeal.dual`: The dual fractional ideal under the trace form.
- `differentIdeal`: The different ideal.

## Main result
- `pow_sub_one_dvd_differentIdeal`:
  If `P ^ e ‚à£ p.map (algebraMap A B)`, then `P ^ (e - 1) ‚à£ differentIdeal A B`.
- `conductor_mul_differentIdeal`:
  If `L = K[x]`, with `x` integral over `A`, then `ùî£ * ùîá = (f'(x))`
    with `f` being the minimal polynomial of `x`.
- `aeval_derivative_mem_differentIdeal`:
  If `L = K[x]`, with `x` integral over `A`, then `f'(x) ‚àà differentIdeal A B`
    with `f` being the minimal polynomial of `x`.

-/
universe u

attribute [local instance] FractionRing.liftAlgebra FractionRing.isScalarTower_liftAlgebra

variable (A K) {B} {L : Type u} [CommRing A] [Field K] [CommRing B] [Field L]
variable [Algebra A K] [Algebra B L] [Algebra A B] [Algebra K L] [Algebra A L]
variable [IsScalarTower A K L] [IsScalarTower A B L]
variable [IsDomain A] [IsDomain B]
variable [IsFractionRing A K] [IsIntegralClosure B A L] [IsFractionRing B L]
variable [FiniteDimensional K L] [IsSeparable K L]

open nonZeroDivisors IsLocalization Matrix Algebra

def traceFormDualSubmodule (I : Submodule B L) : Submodule B L where
  carrier := { s | ‚àÄ a ‚àà I, Algebra.traceForm K L s a ‚àà (algebraMap A K).range }
  add_mem' := fun hx hy a ha ‚Ü¶
    BilinForm.add_left (B := Algebra.traceForm K L) _ _ _ ‚ñ∏ add_mem (hx a ha) (hy a ha)
  zero_mem' := fun a _ ‚Ü¶ BilinForm.zero_left (B := Algebra.traceForm K L) a ‚ñ∏ zero_mem _
  smul_mem' := fun c {x} hx a ha ‚Ü¶ by
    rw [Algebra.traceForm_apply, smul_mul_assoc, mul_comm, ‚Üê smul_mul_assoc, mul_comm]
    exact hx _ (Submodule.smul_mem _ c ha)

variable {A K}

lemma mem_traceFormDualSubmodule {I : Submodule B L} {x} :
    x ‚àà traceFormDualSubmodule A K I ‚Üî
      ‚àÄ a ‚àà I, Algebra.traceForm K L x a ‚àà (algebraMap A K).range :=
  Iff.rfl

lemma le_traceFormDualSubmodule {I J : Submodule B L} :
  I ‚â§ traceFormDualSubmodule A K J ‚Üî ((I * J : Submodule B L).restrictScalars A).map
    ((Algebra.trace K L).restrictScalars A) ‚â§ 1 := by
  rw [Submodule.map_le_iff_le_comap, Submodule.restrictScalars_mul, Submodule.mul_le]
  simp [SetLike.le_def, traceFormDualSubmodule]

lemma mem_traceFormDualSubmodule_iff_isIntegral [IsIntegrallyClosed A] {I : Submodule B L} {x} :
    x ‚àà traceFormDualSubmodule A K I ‚Üî ‚àÄ a ‚àà I, IsIntegral A (Algebra.traceForm K L x a) :=
  forall‚ÇÇ_congr (fun _ _ ‚Ü¶ IsIntegrallyClosed.isIntegral_iff.symm)

variable (A K)

lemma map_equiv_traceFormDualSubmodule [NoZeroSMulDivisors A B] (I : Submodule B (FractionRing B)) :
    (traceFormDualSubmodule A (FractionRing A) I).map (FractionRing.algEquiv B L) =
      traceFormDualSubmodule A K (I.map (FractionRing.algEquiv B L)) := by
  show Submodule.map (FractionRing.algEquiv B L).toLinearEquiv.toLinearMap _ =
    traceFormDualSubmodule A K (I.map (FractionRing.algEquiv B L).toLinearEquiv.toLinearMap)
  rw [Submodule.map_equiv_eq_comap_symm, Submodule.map_equiv_eq_comap_symm]
  ext x
  simp only [AlgEquiv.toLinearEquiv_symm, AlgEquiv.toLinearEquiv_toLinearMap,
    traceFormDualSubmodule, traceForm_apply, Submodule.mem_comap, AlgEquiv.toLinearMap_apply,
    Submodule.mem_mk, AddSubmonoid.mem_mk, AddSubsemigroup.mem_mk, Set.mem_setOf_eq]
  apply (FractionRing.algEquiv B L).forall_congr
  simp only [AlgEquiv.toEquiv_eq_coe, EquivLike.coe_coe, AlgEquiv.symm_apply_apply]
  letI := ((algebraMap (FractionRing A) (FractionRing B)).comp
    (FractionRing.algEquiv A K).symm.toRingEquiv.toRingHom).toAlgebra
  have : IsScalarTower A K (FractionRing B)
  ¬∑ apply IsScalarTower.of_algebraMap_eq'
    ext
    rw [RingHom.algebraMap_toAlgebra]
    simp only [AlgEquiv.toRingEquiv_eq_coe, AlgEquiv.coe_ringEquiv, RingEquiv.toRingHom_eq_coe,
      RingHom.coe_comp, RingHom.coe_coe, Function.comp_apply, AlgEquiv.commutes,
      ‚Üê IsScalarTower.algebraMap_apply]
  let f' := AlgHom.ofLinearMap (((FractionRing.algEquiv B L).restrictScalars
    A).toLinearMap.extendScalarsOfIsLocalization A‚Å∞ K) (FractionRing.algEquiv B L).map_one
    (FractionRing.algEquiv B L).map_mul
  let f : FractionRing B ‚âÉ‚Çê[K] L := { FractionRing.algEquiv B L with commutes' := f'.commutes' }
  have hf : ‚àÄ x, f x = FractionRing.algEquiv B L x := fun _ ‚Ü¶ rfl
  refine fun {y} ‚Ü¶ (forall_congr' $ fun hy ‚Ü¶ ?_)
  rw [‚Üê trace_eq_of_ringEquiv (C := FractionRing B)
    (FractionRing.algEquiv A K).symm.toRingEquiv rfl,
    ‚Üê trace_eq_of_algEquiv f, hf]
  simp only [AlgEquiv.toRingEquiv_eq_coe, _root_.map_mul,
    AlgEquiv.apply_symm_apply, AlgEquiv.coe_ringEquiv]
  rw [‚Üê (FractionRing.algEquiv A K).toAlgHom.comp_algebraMap, ‚Üê RingHom.map_range,
    AlgEquiv.toAlgHom_eq_coe, AlgEquiv.coe_ringHom_commutes, Subring.mem_map_equiv]
  rfl

variable {A K}
variable [IsIntegrallyClosed A]

lemma isIntegral_discr_mul_of_mem_traceFormDualSubmodule
    (I : Submodule B L) {Œπ} [DecidableEq Œπ] [Fintype Œπ]
    (b : Basis Œπ K L) (hb : ‚àÄ i, IsIntegral A (b i))
    (a x : L) (ha : a ‚àà I) (hx : x ‚àà traceFormDualSubmodule A K I) :
    IsIntegral A ((Algebra.discr K b) ‚Ä¢ a * x) := by
  have hinv : IsUnit (traceMatrix K b).det := by
    simpa [‚Üê discr_def] using discr_isUnit_of_basis _ b
  have H := mulVec_cramer (traceMatrix K b) fun i => Algebra.trace K L (x * a * b i)
  rw [‚Üê traceMatrix_of_basis_mulVec, ‚Üê mulVec_smul, (Matrix.mulVec_injective _ hinv).eq_iff,
    traceMatrix_of_basis_mulVec] at H
  rw [‚Üê b.equivFun.symm_apply_apply (_ * _), b.equivFun_symm_apply]
  apply IsIntegral.sum
  intro i _
  rw [smul_mul_assoc, b.equivFun.map_smul, discr_def, mul_comm, ‚Üê H, Algebra.smul_def]
  refine RingHom.IsIntegralElem.mul _ ?_ (hb _)
  apply IsIntegral.algebraMap
  rw [cramer_apply]
  apply IsIntegral.det
  intros j k
  rw [updateColumn_apply]
  split
  ¬∑ rw [mul_assoc]
    rw [mem_traceFormDualSubmodule_iff_isIntegral] at hx
    apply hx
    have ‚ü®y, hy‚ü© := (IsIntegralClosure.isIntegral_iff (A := B)).mp (hb j)
    rw [mul_comm, ‚Üê hy, ‚Üê Algebra.smul_def]
    exact I.smul_mem _ (ha)
  ¬∑ exact isIntegral_trace (RingHom.IsIntegralElem.mul _ (hb j) (hb k))

variable (A K)

-- TODO: merge with FractionalIdeal.dual
def FractionalIdeal.dual‚ÇÇ (I : FractionalIdeal B‚Å∞ L) (hI : I ‚â† 0) :
    FractionalIdeal B‚Å∞ L :=
  ‚ü®traceFormDualSubmodule A K I, by
    classical
    have ‚ü®s, b, hb‚ü© := FiniteDimensional.exists_is_basis_integral A K L
    have := IsIntegralClosure.isFractionRing_of_finite_extension A K L B
    obtain ‚ü®x, hx, hx'‚ü© := exists_ne_zero_mem_isInteger hI
    have ‚ü®y, hy‚ü© := (IsIntegralClosure.isIntegral_iff (A := B)).mp
      (IsIntegral.algebraMap (B := L) (discr_isIntegral K hb))
    refine ‚ü®y * x, mem_nonZeroDivisors_iff_ne_zero.mpr (mul_ne_zero ?_ hx), ?_‚ü©
    ¬∑ rw [‚Üê (IsIntegralClosure.algebraMap_injective B A L).ne_iff, hy, RingHom.map_zero,
        ‚Üê (algebraMap K L).map_zero, (algebraMap K L).injective.ne_iff]
      exact discr_not_zero_of_basis K b
    ¬∑ intros z hz
      convert isIntegral_discr_mul_of_mem_traceFormDualSubmodule I b hb _ z hx' hz using 1
      ¬∑ ext w
        exact (IsIntegralClosure.isIntegral_iff (A := B)).symm
      ¬∑ rw [Algebra.smul_def, RingHom.map_mul, hy, ‚Üê Algebra.smul_def]‚ü©

variable {A K}

lemma FractionalIdeal.mem_dual‚ÇÇ {I : FractionalIdeal B‚Å∞ L} {hI : I ‚â† 0} {x} :
  x ‚àà dual‚ÇÇ A K I hI ‚Üî ‚àÄ a ‚àà I, Algebra.traceForm K L x a ‚àà (algebraMap A K).range := Iff.rfl

lemma FractionalIdeal.dual‚ÇÇ_ne_zero {I : FractionalIdeal B‚Å∞ L} {hI : I ‚â† 0} :
    dual‚ÇÇ A K I hI ‚â† 0 := by
  obtain ‚ü®b, hb, hb'‚ü© := I.prop
  suffices algebraMap B L b ‚àà dual‚ÇÇ A K I hI by
    intro e
    rw [e, mem_zero_iff, ‚Üê (algebraMap B L).map_zero,
      (IsIntegralClosure.algebraMap_injective B A L).eq_iff] at this
    exact mem_nonZeroDivisors_iff_ne_zero.mp hb this
  intro a ha
  apply IsIntegrallyClosed.isIntegral_iff.mp
  apply isIntegral_trace
  dsimp
  convert hb' a ha using 1
  ¬∑ ext w
    exact IsIntegralClosure.isIntegral_iff (A := B)
  ¬∑ exact (Algebra.smul_def _ _).symm

lemma FractionalIdeal.le_dual‚ÇÇ_inv_aux (I J: FractionalIdeal B‚Å∞ L) (hI) (hIJ : I * J ‚â§ 1) :
    J ‚â§ dual‚ÇÇ A K I hI := by
  intro x hx y hy
  apply IsIntegrallyClosed.isIntegral_iff.mp
  apply isIntegral_trace
  rw [IsIntegralClosure.isIntegral_iff (A := B)]
  have ‚ü®z, _, hz‚ü© := hIJ (FractionalIdeal.mul_mem_mul hy hx)
  rw [mul_comm] at hz
  exact ‚ü®z, hz‚ü©

variable [IsDedekindDomain B] [IsFractionRing B L]

variable (A K)
variable (B)
variable (L)

lemma pow_sub_one_dvd_differentIdeal_aux
    [IsDedekindDomain A] [NoZeroSMulDivisors A B] [Module.Finite A B]
    {p : Ideal A} [p.IsMaximal] (P : Ideal B) (e : ‚Ñï) (he : 0 < e) (hp : p ‚â† ‚ä•)
    (hP : P ^ e ‚à£ p.map (algebraMap A B)) : P ^ (e - 1) ‚à£ differentIdeal A B := by
  obtain ‚ü®a, ha‚ü© := (pow_dvd_pow _ (Nat.sub_le e 1)).trans hP
  have hp' := (Ideal.map_eq_bot_iff_of_injective
    (NoZeroSMulDivisors.algebraMap_injective A B)).not.mpr hp
  have habot : a ‚â† ‚ä• := fun ha' ‚Ü¶ hp' (by simpa [ha'] using ha)
  have hPbot : P ‚â† ‚ä•
  ¬∑ rintro rfl; apply hp'
    rwa [‚Üê Ideal.zero_eq_bot, zero_pow he, zero_dvd_iff, Ideal.zero_eq_bot] at hP
  have : p.map (algebraMap A B) ‚à£ a ^ e
  ¬∑ obtain ‚ü®b, hb‚ü© := hP
    apply_fun (¬∑ ^ e : Ideal B ‚Üí _) at ha
    apply_fun (¬∑ ^ (e - 1) : Ideal B ‚Üí _) at hb
    simp only [mul_pow, ‚Üê pow_mul, mul_comm e] at ha hb
    conv_lhs at ha => rw [‚Üê Nat.sub_add_cancel (Nat.one_le_iff_ne_zero.mpr he.ne.symm)]
    rw [pow_add, hb, mul_assoc, mul_right_inj' (pow_ne_zero _ hPbot), pow_one, mul_comm] at ha
    exact ‚ü®_, ha.symm‚ü©
  suffices : ‚àÄ x ‚àà a, Algebra.intTrace A B x ‚àà p
  ¬∑ have hP : ((P ^ (e - 1) : _)‚Åª¬π : FractionalIdeal B‚Å∞ L) = a / p.map (algebraMap A B)
    ¬∑ apply inv_involutive.injective
      simp only [inv_inv, ha, FractionalIdeal.coeIdeal_mul, inv_div, ne_eq,
        FractionalIdeal.coeIdeal_eq_zero, mul_div_assoc]
      rw [div_self (by simpa), mul_one]
    rw [Ideal.dvd_iff_le, differentialIdeal_le_iff (K := K) (L := L) (pow_ne_zero _ hPbot), hP,
      Submodule.map_le_iff_le_comap]
    intro x hx
    rw [Submodule.restrictScalars_mem, FractionalIdeal.mem_coe,
      FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp')] at hx
    rw [Submodule.mem_comap, LinearMap.coe_restrictScalars, ‚Üê FractionalIdeal.coe_one,
      ‚Üê div_self (G‚ÇÄ := FractionalIdeal A‚Å∞ K) (a := p) (by simpa using hp),
      FractionalIdeal.mem_coe, FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp)]
    simp only [FractionalIdeal.mem_coeIdeal, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ] at hx
    intro y hy'
    obtain ‚ü®y, hy, rfl : algebraMap A K _ = _‚ü© := (FractionalIdeal.mem_coeIdeal _).mp hy'
    obtain ‚ü®z, hz, hz'‚ü© := hx _ (Ideal.mem_map_of_mem _ hy)
    have : Algebra.trace K L (algebraMap B L z) ‚àà (p : FractionalIdeal A‚Å∞ K)
    ¬∑ rw [‚Üê Algebra.algebraMap_intTrace (R := A)]
      exact Submodule.mem_map_of_mem (this z hz)
    rwa [mul_comm, ‚Üê smul_eq_mul, ‚Üê LinearMap.map_smul, Algebra.smul_def, mul_comm,
      ‚Üê IsScalarTower.algebraMap_apply, IsScalarTower.algebraMap_apply A B L, ‚Üê hz']
  intros x hx
  rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Algebra.trace_quotient_eq_of_isDedekindDomain,
    ‚Üê isNilpotent_iff_eq_zero]
  apply Algebra.isNilpotent_trace_of_isNilpotent
  use e
  rw [‚Üê map_pow, Ideal.Quotient.eq_zero_iff_mem]
  apply (Ideal.dvd_iff_le.mp this)
  exact Ideal.pow_mem_pow hx _

lemma pow_sub_one_dvd_differentIdeal
    [IsDedekindDomain A] [NoZeroSMulDivisors A B] [Module.Finite A B]
    [IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} [p.IsMaximal] (P : Ideal B) (e : ‚Ñï) (hp : p ‚â† ‚ä•)
    (hP : P ^ e ‚à£ p.map (algebraMap A B)) : P ^ (e - 1) ‚à£ differentIdeal A B := by
  have : IsIntegralClosure B A (FractionRing B) :=
    isIntegralClosure_of_isIntegrallyClosed _ _ _ (Algebra.IsIntegral.of_finite (R := A) (B := B))
  have : IsLocalization (algebraMapSubmonoid B A‚Å∞) (FractionRing B) :=
    IsIntegralClosure.isLocalization' _ (FractionRing A) _ _
      (isAlgebraic_of_isFractionRing _ _ (Algebra.IsIntegral.of_finite (R := A) (B := B)))
  have : FiniteDimensional (FractionRing A) (FractionRing B) :=
    Module.Finite_of_isLocalization A B _ _ A‚Å∞
  by_cases he : e = 0
  ¬∑ rw [he, pow_zero]; exact one_dvd _
  rw [‚Üê Ne.def, ‚Üê Nat.pos_iff_ne_zero] at he
  exact pow_sub_one_dvd_differentIdeal_aux A (FractionRing A) B (FractionRing B) P e he hp hP

lemma traceFormDualSubmodule_eq_span_dualBasis {Œπ} [Fintype Œπ] [DecidableEq Œπ]
    (b : Basis Œπ K L) :
    traceFormDualSubmodule A K (Submodule.span A (Set.range b)) =
      Submodule.span A (Set.range ((Algebra.traceForm K L).dualBasis
        (traceForm_nondegenerate K L) b)) := by
  apply le_antisymm
  ¬∑ intros x hx
    rw [‚Üê ((Algebra.traceForm K L).dualBasis (traceForm_nondegenerate K L) b).sum_repr x]
    apply sum_mem
    rintro i -
    obtain ‚ü®a, ha : _ = Algebra.trace K L (x * b i)‚ü© :=
      hx (b i) (Submodule.subset_span (Set.mem_range_self _))
    simp only [BilinForm.dualBasis_repr_apply, traceForm_apply, ‚Üê ha, algebraMap_smul]
    apply Submodule.smul_mem _ a (Submodule.subset_span (Set.mem_range_self _))
  ¬∑ rw [Submodule.span_le]
    rintro _ ‚ü®i, rfl‚ü© a ha
    rw [‚Üê Fintype.range_total (S := A) A] at ha
    obtain ‚ü®v, rfl‚ü© := ha
    rw [Fintype.total_apply, traceForm_apply, Finset.mul_sum, map_sum]
    apply sum_mem
    rintro i -
    rw [mul_comm, smul_mul_assoc, LinearMap.map_smul_of_tower, mul_comm, ‚Üê traceForm_apply,
      BilinForm.apply_dualBasis_left, ‚Üê SetLike.mem_coe, RingHom.coe_range, ‚Üê Algebra.coe_bot]
    refine (‚ä• : Subalgebra A K).smul_mem ?_ (v i)
    split; exact one_mem _; exact zero_mem _

open Pointwise Polynomial in
lemma traceFormDualSubmodule_span_adjoin
    (x : L) (hx : Algebra.adjoin K {x} = ‚ä§) (hAx : IsIntegral A x) :
    traceFormDualSubmodule A K (Subalgebra.toSubmodule (Algebra.adjoin A {x})) =
      (aeval x (derivative <| minpoly K x) : L)‚Åª¬π ‚Ä¢
        (Subalgebra.toSubmodule (Algebra.adjoin A {x})) := by
  classical
  have hKx : IsIntegral K x := Algebra.IsIntegral.of_finite (R := K) (B := L) x
  let pb := (Algebra.adjoin.powerBasis' hKx).map
    ((Subalgebra.equivOfEq _ _ hx).trans (Subalgebra.topEquiv))
  have pbgen : pb.gen = x := by simp
  have hpb : ‚áë(BilinForm.dualBasis (traceForm K L) _ pb.basis) = _ :=
    _root_.funext (traceForm_dualBasis_powerBasis_eq pb)
  have : (Subalgebra.toSubmodule (Algebra.adjoin A {x})) = Submodule.span A (Set.range pb.basis)
  ¬∑ rw [‚Üê span_range_natDegree_eq_adjoin _ hAx]
    congr; ext y
    have : natDegree (minpoly A x) = natDegree (minpoly K x)
    ¬∑ rw [minpoly.isIntegrallyClosed_eq_field_fractions' K hAx, (minpoly.monic hAx).natDegree_map]
    simp only [Finset.coe_image, Finset.coe_range, Set.mem_image, Set.mem_Iio, Set.mem_range,
      pb.basis_eq_pow, pbgen]
    simp only [PowerBasis.map_dim, adjoin.powerBasis'_dim, this]
    exact ‚ü®fun ‚ü®a, b, c‚ü© ‚Ü¶ ‚ü®‚ü®a, b‚ü©, c‚ü©, fun ‚ü®‚ü®a, b‚ü©, c‚ü© ‚Ü¶ ‚ü®a, b, c‚ü©‚ü©
  clear_value pb
  conv_lhs => rw [this]
  rw [‚Üê span_coeff_minpolyDiv hAx, traceFormDualSubmodule_eq_span_dualBasis A K L pb.basis,
    Submodule.smul_span, hpb]
  show _ = Submodule.span A (_ '' _)
  simp only [‚Üê Set.range_comp, smul_eq_mul, div_eq_inv_mul, pbgen,
    minpolyDiv_eq_of_isIntegrallyClosed K hAx]
  apply le_antisymm <;> rw [Submodule.span_le]
  ¬∑ rintro _ ‚ü®i, rfl‚ü©; exact Submodule.subset_span ‚ü®i, rfl‚ü©
  ¬∑ rintro _ ‚ü®i, rfl‚ü©
    by_cases hi : i < pb.dim
    ¬∑ exact Submodule.subset_span ‚ü®‚ü®i, hi‚ü©, rfl‚ü©
    ¬∑ rw [Function.comp_apply, coeff_eq_zero_of_natDegree_lt, mul_zero]; exact zero_mem _
      rw [‚Üê pb.natDegree_minpoly, pbgen, ‚Üê natDegree_minpolyDiv_succ hKx,
        ‚Üê Nat.succ_eq_add_one] at hi
      exact le_of_not_lt hi

open Polynomial Pointwise in
lemma conductor_mul_differentIdeal [NoZeroSMulDivisors A B]
    (x : B) (hx : Algebra.adjoin K {algebraMap B L x} = ‚ä§) :
    (conductor A x) * differentIdeal A B = Ideal.span {aeval x (derivative (minpoly A x))} := by
  classical
  have onz := one_ne_zero (Œ± := FractionalIdeal B‚Å∞ L)
  have hAx : IsIntegral A x := IsIntegralClosure.isIntegral A L x
  haveI := IsIntegralClosure.isFractionRing_of_finite_extension A K L B
  apply FractionalIdeal.coeIdeal_injective (K := L)
  simp only [FractionalIdeal.coeIdeal_mul, FractionalIdeal.coeIdeal_span_singleton]
  rw [coeIdeal_differentIdeal A K L B,
    mul_inv_eq_iff_eq_mul‚ÇÄ (FractionalIdeal.dual_ne_zero A K onz)]
  apply FractionalIdeal.coeToSubmodule_injective
  simp only [FractionalIdeal.coe_coeIdeal, FractionalIdeal.coe_mul,
    FractionalIdeal.coe_spanSingleton, Submodule.span_singleton_mul]
  ext y
  have hne‚ÇÅ : aeval (algebraMap B L x) (derivative (minpoly K (algebraMap B L x))) ‚â† 0
  ¬∑ exact aeval_derivative_minpoly_ne_zero _ (IsSeparable.separable _ _)
  have : algebraMap B L (aeval x (derivative (minpoly A x))) ‚â† 0
  ¬∑ rwa [minpoly.isIntegrallyClosed_eq_field_fractions K L hAx, derivative_map,
      aeval_map_algebraMap, aeval_algebraMap_apply] at hne‚ÇÅ
  rw [Submodule.mem_smul_iff_inv this, FractionalIdeal.mem_coe, FractionalIdeal.mem_dual onz,
    mem_coeIdeal_conductor]
  have hne‚ÇÇ : (aeval (algebraMap B L x) (derivative (minpoly K (algebraMap B L x))))‚Åª¬π ‚â† 0
  ¬∑ rwa [ne_eq, inv_eq_zero]
  have : IsIntegral A (algebraMap B L x) := IsIntegral.map (IsScalarTower.toAlgHom A B L) hAx
  simp_rw [‚Üê Subalgebra.mem_toSubmodule, ‚Üê Submodule.mul_mem_smul_iff (y := y * _)
    (mem_nonZeroDivisors_of_ne_zero hne‚ÇÇ)]
  rw [‚Üê traceFormDualSubmodule_span_adjoin A K L _ hx this]
  simp only [mem_traceFormDualSubmodule, traceForm_apply, Subalgebra.mem_toSubmodule,
    minpoly.isIntegrallyClosed_eq_field_fractions K L hAx,
    derivative_map, aeval_map_algebraMap, aeval_algebraMap_apply, mul_assoc,
    FractionalIdeal.mem_one_iff, forall_exists_index, forall_apply_eq_imp_iff]
  simp_rw [‚Üê IsScalarTower.toAlgHom_apply A B L x, ‚Üê Algebra.map_adjoin_singleton]
  simp only [Subalgebra.mem_map, IsScalarTower.coe_toAlgHom',
    forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ, ‚Üê _root_.map_mul]
  exact ‚ü®fun H b ‚Ü¶ (mul_one b) ‚ñ∏ H b 1 (one_mem _), fun H _ _ _ ‚Ü¶ H _‚ü©

open Polynomial Pointwise in
lemma aeval_derivative_mem_differentIdeal [NoZeroSMulDivisors A B]
    (x : B) (hx : Algebra.adjoin K {algebraMap B L x} = ‚ä§) :
    aeval x (derivative (minpoly A x)) ‚àà differentIdeal A B := by
  refine SetLike.le_def.mp ?_ (Ideal.mem_span_singleton_self _)
  rw [‚Üê conductor_mul_differentIdeal A K B L x hx]
  exact Ideal.mul_le_left
