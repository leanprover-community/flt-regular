import Mathlib.NumberTheory.Cyclotomic.Rat
import FltRegular.NumberTheory.Cyclotomic.Factoring
import FltRegular.NumberTheory.Cyclotomic.UnitLemmas
import FltRegular.NumberTheory.Cyclotomic.CyclRat
import Mathlib.RingTheory.Ideal.Norm
import Mathlib.RingTheory.ClassGroup
import FltRegular.ReadyForMathlib.PowerBasis
import FltRegular.NumberTheory.AuxLemmas

variable {K : Type*} {p : â„•+} [hpri : Fact p.Prime] [Field K] [CharZero K] [IsCyclotomicExtension {p} â„š K]

variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p)

open scoped BigOperators nonZeroDivisors NumberField
open Polynomial

variable (hp : p â‰  2)

lemma IsPrimitiveRoot.prime_span_sub_one : Prime (Ideal.span <| singleton <| (hÎ¶.unit' - 1 : ğ“ K)) := by
  haveI : Fact (Nat.Prime p) := hpri
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rw [Ideal.prime_iff_isPrime,
    Ideal.span_singleton_prime (hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt)]
  exact hÎ¶.zeta_sub_one_prime'
  Â· rw [Ne.def, Ideal.span_singleton_eq_bot]
    exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

lemma norm_Int_zeta_sub_one : Algebra.norm â„¤ (â†‘(IsPrimitiveRoot.unit' hÎ¶) - 1 : ğ“ K) = p := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  haveI : Fact (Nat.Prime p) := hpri
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  simp [Algebra.coe_norm_int, hÎ¶.sub_one_norm_prime (cyclotomic.irreducible_rat p.2) hp]

@[simp]
lemma PNat.coe_two : (2 : â„•+) = (2 : â„•) := rfl

lemma surjective_of_isCyclotomicExtension_two (R S) [CommRing R] [CommRing S]
    [IsDomain S] [Algebra R S] [IsCyclotomicExtension {2} R S] :
    Function.Surjective (algebraMap R S) := by
  intro x
  have := IsCyclotomicExtension.adjoin_roots (S := {2}) (A := R) (B := S) x
  simp only [Set.mem_singleton_iff, exists_eq_left, sq_eq_one_iff, PNat.coe_two] at this
  have H : Algebra.adjoin R {b : S | b = 1 âˆ¨ b = -1} â‰¤ âŠ¥
  Â· rw [Algebra.adjoin_le_iff]
    rintro _ (rfl|rfl)
    Â· exact one_mem _
    Â· exact neg_mem (one_mem _)
  exact H this

theorem IsPrimitiveRoot.sub_one_norm_two' {K L} [Field K] [Field L] [Algebra K L] {Î¶ : L}
    (hÎ¶ : IsPrimitiveRoot Î¶ 2)
    [IsCyclotomicExtension {2} K L] : Algebra.norm K (Î¶ - 1) = -2 := by
  rw [hÎ¶.eq_neg_one_of_two_right]
  suffices : Algebra.norm K (algebraMap K L (-2)) = -2
  Â· simpa only [sub_eq_add_neg, â† one_add_one_eq_two,
      neg_add_rev, map_add, map_neg, map_one] using this
  rw [Algebra.norm_algebraMap, finrank_eq_one_iff'.mpr, pow_one]
  refine âŸ¨1, one_ne_zero, fun w â†¦ ?_âŸ©
  simpa only [Algebra.algebraMap_eq_smul_one] using surjective_of_isCyclotomicExtension_two K L w

lemma norm_Int_zeta_sub_one' (hp : p = 2) :
    Algebra.norm â„¤ (â†‘(IsPrimitiveRoot.unit' hÎ¶) - 1 : ğ“ K) = -p := by
  clear â€¹p â‰  2â€º
  letI := IsCyclotomicExtension.numberField {p} â„š K
  haveI : Fact (Nat.Prime p) := hpri
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  subst hp
  simp [Algebra.coe_norm_int, hÎ¶.sub_one_norm_two']

lemma associated_zeta_sub_one_pow_prime : Associated ((hÎ¶.unit' - 1 : ğ“ K) ^ (p - 1 : â„•)) p := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  haveI : Fact (Nat.Prime p) := hpri
  rw [â† eval_one_cyclotomic_prime (R := ğ“ K) (p := p),
    cyclotomic_eq_prod_X_sub_primitiveRoots hÎ¶.unit'_coe, eval_prod]
  simp only [eval_sub, eval_X, eval_C]
  rw [â† Nat.totient_prime this.out, â† hÎ¶.unit'_coe.card_primitiveRoots, â† Finset.prod_const]
  apply Associated.prod
  intro Î· hÎ·
  exact hÎ¶.unit'_coe.associated_sub_one hpri.out
    (one_mem_nthRootsFinset this.out.pos)
    ((isPrimitiveRoot_of_mem_primitiveRoots hÎ·).mem_nthRootsFinset hpri.out.pos)
      ((isPrimitiveRoot_of_mem_primitiveRoots hÎ·).ne_one hpri.out.one_lt).symm

lemma isCoprime_of_not_zeta_sub_one_dvd (hx : Â¬ (hÎ¶.unit' : ğ“ K) - 1 âˆ£ x) : IsCoprime â†‘p x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rwa [â† Ideal.isCoprime_span_singleton_iff,
    â† Ideal.span_singleton_eq_span_singleton.mpr (associated_zeta_sub_one_pow_prime hÎ¶),
    â† Ideal.span_singleton_pow, IsCoprime.pow_left_iff, Ideal.isCoprime_iff_gcd,
    hÎ¶.prime_span_sub_one.irreducible.gcd_eq_one_iff, Ideal.dvd_span_singleton,
    Ideal.mem_span_singleton]
  Â· simpa only [ge_iff_le, tsub_pos_iff_lt] using hpri.out.one_lt

set_option synthInstance.maxHeartbeats 40000 in
lemma exists_zeta_sub_one_dvd_sub_Int (a : ğ“ K) : âˆƒ b : â„¤, (hÎ¶.unit' - 1: ğ“ K) âˆ£ a - b := by
  letI : AddGroup (ğ“ K â§¸ Ideal.span (singleton (hÎ¶.unit' - 1: ğ“ K))) := inferInstance
  letI : Fact (Nat.Prime p) := hpri
  simp_rw [â† Ideal.Quotient.eq_zero_iff_dvd, map_sub, sub_eq_zero, â† SModEq.Ideal_def]
  convert exists_int_sModEq hÎ¶.subOneIntegralPowerBasis' a
  rw [hÎ¶.subOneIntegralPowerBasis'_gen]
  rw [Subtype.ext_iff, AddSubgroupClass.coe_sub, IsPrimitiveRoot.val_unit'_coe, OneMemClass.coe_one]
  rfl

lemma exists_dvd_pow_sub_Int_pow (a : ğ“ K) : âˆƒ b : â„¤, â†‘p âˆ£ a ^ (p : â„•) - (b : ğ“ K) ^ (p : â„•) := by
  obtain âŸ¨Î¶, hÎ¶âŸ© := IsCyclotomicExtension.exists_prim_root â„š (B := K) (Set.mem_singleton p)
  obtain âŸ¨b, k, eâŸ© := exists_zeta_sub_one_dvd_sub_Int hÎ¶ a
  obtain âŸ¨r, hrâŸ© := exists_add_pow_prime_eq hpri.out a (-b)
  obtain âŸ¨u, huâŸ© := (associated_zeta_sub_one_pow_prime hÎ¶).symm
  rw [(Nat.Prime.odd_of_ne_two hpri.out (PNat.coe_injective.ne hp)).neg_pow, â† sub_eq_add_neg, e,
    mul_pow, â† sub_eq_add_neg] at hr
  nth_rw 1 [â† Nat.sub_add_cancel (n := p) (m := 1) hpri.out.one_lt.le] at hr
  rw [pow_succ', â† hu, mul_assoc, mul_assoc] at hr
  use b, â†‘u * ((hÎ¶.unit' - 1 : ğ“ K) * k ^ (p : â„•)) - r
  rw [mul_sub, hr, add_sub_cancel]

lemma norm_dvd_iff {R : Type*} [CommRing R] [IsDomain R] [IsDedekindDomain R]
    [Infinite R] [Module.Free â„¤ R] [Module.Finite â„¤ R] (x : R) (hx : Prime (Algebra.norm â„¤ x)) {y : â„¤} :
    Algebra.norm â„¤ x âˆ£ y â†” x âˆ£ y := by
  rw [â† Ideal.mem_span_singleton (y := x), â† eq_intCast (algebraMap â„¤ R), â† Ideal.mem_comap,
    â† Ideal.span_singleton_absNorm, Ideal.mem_span_singleton, Ideal.absNorm_span_singleton,
    Int.natAbs_dvd]
  rwa [Ideal.absNorm_span_singleton, â† Int.prime_iff_natAbs_prime]

section

variable {Î±} [CommMonoidWithZero Î±]

theorem prime_units_mul (a : Î±Ë£) (b : Î±) : Prime (â†‘a * b) â†” Prime b := by simp [Prime]

theorem prime_isUnit_mul {a b : Î±} (h : IsUnit a) : Prime (a * b) â†” Prime b :=
  let âŸ¨a, haâŸ© := h
  ha â–¸ prime_units_mul a b

theorem prime_mul_units (a : Î±Ë£) (b : Î±) : Prime (b * â†‘a) â†” Prime b := by simp [Prime]

theorem prime_mul_isUnit {a b : Î±} (h : IsUnit a) : Prime (b * a) â†” Prime b :=
  let âŸ¨a, haâŸ© := h
  ha â–¸ prime_mul_units a b

theorem prime_neg_iff {Î±} [CommRing Î±] {a : Î±} : Prime (-a) â†” Prime a := by
  rw [â† prime_isUnit_mul isUnit_one.neg, neg_mul, one_mul, neg_neg]

theorem prime_mul_iff {Î±} [CancelCommMonoidWithZero Î±] {a b : Î±} :
    Prime (a * b) â†” Prime a âˆ§ IsUnit b âˆ¨ Prime b âˆ§ IsUnit a := by
  constructor
  Â· intro h
    have ha : a â‰  0 := fun ha â†¦ by simp [ha] at h
    have hb : b â‰  0 := fun hb â†¦ by simp [hb] at h
    have : a * b âˆ£ a * 1 âˆ¨ a * b âˆ£ 1 * b := by simpa using h.2.2 _ _ dvd_rfl
    rw [mul_dvd_mul_iff_left ha, mul_dvd_mul_iff_right hb,
      â† isUnit_iff_dvd_one, â† isUnit_iff_dvd_one] at this
    refine this.imp (fun h' => âŸ¨?_, h'âŸ©) (fun h' => âŸ¨?_, h'âŸ©)
    Â· rwa [prime_mul_isUnit h'] at h
    Â· rwa [prime_isUnit_mul h'] at h
  Â· rintro (âŸ¨ha, hbâŸ© | âŸ¨hb, haâŸ©)
    Â· rwa [prime_mul_isUnit hb]
    Â· rwa [prime_isUnit_mul ha]
end

lemma zeta_sub_one_dvd_Int_iff {n : â„¤} : (hÎ¶.unit' : ğ“ K) - 1 âˆ£ n â†” â†‘p âˆ£ n := by
  clear hp
  letI := IsCyclotomicExtension.numberField {p} â„š K
  by_cases hp : p = 2
  Â· rw [â† neg_dvd (a := (p : â„¤))]
    rw [â† norm_Int_zeta_sub_one' hÎ¶ hp, norm_dvd_iff]
    rw [norm_Int_zeta_sub_one' hÎ¶ hp, prime_neg_iff, â† Nat.prime_iff_prime_int]
    exact hpri.out
  rw [â† norm_Int_zeta_sub_one hÎ¶ hp, norm_dvd_iff]
  rw [norm_Int_zeta_sub_one hÎ¶ hp, â† Nat.prime_iff_prime_int]
  exact hpri.out

lemma IsPrimitiveRoot.sub_one_dvd_sub {A : Type*} [CommRing A] [IsDomain A]
    {p : â„•} (hp : p.Prime) {Î¶ : A} (hÎ¶ : IsPrimitiveRoot Î¶ p) {Î·â‚ : A} (hÎ·â‚ : Î·â‚ âˆˆ nthRootsFinset p A)
    {Î·â‚‚ : A} (hÎ·â‚‚ : Î·â‚‚ âˆˆ nthRootsFinset p A) :
    Î¶ - 1 âˆ£ Î·â‚ - Î·â‚‚ := by
  by_cases h : Î·â‚ = Î·â‚‚
  Â· rw [h, sub_self]; exact dvd_zero _
  Â· exact (hÎ¶.associated_sub_one hp hÎ·â‚ hÎ·â‚‚ h).dvd

set_option synthInstance.maxHeartbeats 40000 in
lemma quotient_zero_sub_one_comp_aut (Ïƒ : ğ“ K â†’+* ğ“ K) :
    (Ideal.Quotient.mk (Ideal.span {(hÎ¶.unit' : ğ“ K) - 1})).comp Ïƒ = Ideal.Quotient.mk _ := by
  have : Fact (Nat.Prime p) := hpri
  letI := IsCyclotomicExtension.numberField {p} â„š K
  letI : AddGroup (ğ“ K â§¸ Ideal.span (singleton (hÎ¶.unit' - 1: ğ“ K))) := inferInstance
  apply RingHom.toIntAlgHom_injective
  apply hÎ¶.integralPowerBasis'.algHom_ext
  rw [show hÎ¶.integralPowerBasis'.gen = hÎ¶.unit' from Subtype.ext (by simp)]
  simp only [RingHom.toIntAlgHom, RingHom.toMonoidHom_eq_coe, AlgHom.coe_mk, RingHom.coe_mk,
    MonoidHom.coe_coe, RingHom.coe_comp, RingHom.coe_coe, Function.comp_apply]
  rw [â† sub_eq_zero, â† map_sub, Ideal.Quotient.eq_zero_iff_mem, Ideal.mem_span_singleton]
  apply hÎ¶.unit'_coe.sub_one_dvd_sub hpri.out
  Â· rw [mem_nthRootsFinset p.pos, â† map_pow, hÎ¶.unit'_coe.pow_eq_one, map_one]
  Â· rw [mem_nthRootsFinset p.pos, hÎ¶.unit'_coe.pow_eq_one]

lemma zeta_sub_one_dvd_trace_sub_smul (x : ğ“ K) :
    (hÎ¶.unit' - 1 : ğ“ K) âˆ£ Algebra.trace â„¤ _ x - (p - 1 : â„•) â€¢ x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  letI := IsCyclotomicExtension.isGalois p â„š K
  have : (Algebra.trace â„¤ _ x : ğ“ K) = âˆ‘ Ïƒ : K â‰ƒâ‚[â„š] K, (intGal Ïƒ).toRingHom x
  Â· apply (show Function.Injective (algebraMap (ğ“ K) K) from Subtype.val_injective)
    rw [â† eq_intCast (algebraMap â„¤ (ğ“ K)), â† IsScalarTower.algebraMap_apply,
      IsScalarTower.algebraMap_apply â„¤ â„š K, eq_intCast, Algebra.coe_trace_int,
      trace_eq_sum_automorphisms, map_sum]
    rfl
  rw [â† Ideal.mem_span_singleton, â† Ideal.Quotient.eq_zero_iff_mem, map_sub, this,
    map_sum]
  simp_rw [â† RingHom.comp_apply, quotient_zero_sub_one_comp_aut]
  rw [Finset.sum_const, map_nsmul, sub_eq_zero, Finset.card_univ, IsGalois.card_aut_eq_finrank,
    IsCyclotomicExtension.finrank K (cyclotomic.irreducible_rat p.pos), Nat.totient_prime hpri.out]

lemma zeta_sub_one_pow_dvd_norm_sub_pow (x : ğ“ K) :
    (hÎ¶.unit' - 1 : ğ“ K) ^ (p : â„•) âˆ£
      (Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) : ğ“ K) - 1 + (p : â„•) â€¢ x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  obtain âŸ¨r, hrâŸ© := Algebra.norm_one_add_smul (p : â„¤) x
  obtain âŸ¨s, hsâŸ© := zeta_sub_one_dvd_trace_sub_smul hÎ¶ x
  obtain âŸ¨t, htâŸ© := (associated_zeta_sub_one_pow_prime hÎ¶).dvd
  rw [sub_eq_iff_eq_add] at hs
  simp only [zsmul_eq_mul, Int.cast_ofNat] at hr
  simp only [nsmul_eq_mul, hr, Int.cast_add, Int.cast_one, Int.cast_mul, hs, ge_iff_le, PNat.pos,
    Nat.cast_pred, Int.cast_ofNat, Int.cast_pow, Nat.cast_mul, ne_eq, PNat.ne_zero,
    not_false_eq_true, isUnit_pow_iff]
  suffices : (hÎ¶.unit' - 1 : ğ“ K) ^ (p : â„•) âˆ£ (hÎ¶.unit' - 1) * p * s + (p : ğ“ K) ^ 2 * (r + x)
  Â· convert this using 1; ring
  apply dvd_add
  Â· apply dvd_mul_of_dvd_left
    rw [ht, â† mul_assoc, â† pow_succ, tsub_add_cancel_of_le (Nat.Prime.one_lt hpri.out).le]
    exact dvd_mul_right _ _
  Â· rw [ht, mul_pow, â† pow_mul, mul_assoc]
    apply dvd_mul_of_dvd_left
    apply pow_dvd_pow
    zify [(Nat.Prime.one_lt hpri.out).le]
    linarith only [Nat.Prime.two_le hpri.out]

lemma norm_add_one_smul_of_isUnit {K} [Field K] [NumberField K] {p : â„•} (hpri : p.Prime)
    (hp : p â‰  2) (x : ğ“ K)
    (hx : IsUnit (1 + (p : â„•) â€¢ x)) : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = 1 := by
  have H : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = 1 âˆ¨ Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = -1
  Â· apply Int.natAbs_eq_iff.mp
    apply (Int.cast_injective (Î± := â„š)).comp Nat.cast_injective
    simp only [Int.cast_abs, Function.comp_apply, Nat.cast_one, Int.cast_one, â† Int.abs_eq_natAbs,
      Algebra.coe_norm_int, â† NumberField.isUnit_iff_norm.mp hx, RingOfIntegers.norm_apply_coe]
  have : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) â‰  -1
  Â· intro e; apply hp
    obtain âŸ¨r, hrâŸ© := Algebra.norm_one_add_smul (p : â„¤) x
    have : (p : â„¤) * (- Algebra.trace â„¤ _ x - r * p) = 2
    Â· rw [zsmul_eq_mul, Int.cast_ofNat, â† nsmul_eq_mul, e, eq_comm, â† sub_eq_zero] at hr
      rw [eq_comm, â† sub_eq_zero, â† hr]
      ring
    exact (Nat.prime_two.eq_one_or_self_of_dvd _
      (Int.coe_nat_dvd.mp âŸ¨_, this.symmâŸ©)).resolve_left hpri.ne_one
  exact H.resolve_right this
