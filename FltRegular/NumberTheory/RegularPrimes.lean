import Mathlib.FieldTheory.SplittingField.Construction
import Mathlib.NumberTheory.Bernoulli
import Mathlib.NumberTheory.ClassNumber.Finite
import Mathlib.NumberTheory.ClassNumber.AdmissibleAbs
import FltRegular.NumberTheory.Cyclotomic.CyclRat
import Mathlib.NumberTheory.Cyclotomic.Rat

/-!
# Regular primes

## Main definitions

* `is_regular_number`: a natural number `n` is regular if `n` is coprime with the cardinal of the
  class group.

-/


noncomputable section

open Nat Polynomial

open NumberField

open scoped Classical NumberField

section SafeInstances

/- The idea of `open_locale cyclotomic` is that it provides some of these instances when needed,
but sadly its implementation is so unsafe that using it here creates a lot of diamonds.
We instead put some safe specialised instances here, and we can maybe look at generalising them
later, when this is needed. Most results from here on genuinely only work for ‚Ñö, so this is
very fine for the moment. -/
-- todo: now the diamond is fixed, `open_locale cyclotomic` may be fine.
instance safe {p : ‚Ñï+} : NumberField (CyclotomicField p ‚Ñö) :=
  IsCyclotomicExtension.numberField {p} ‚Ñö <| CyclotomicField p ‚Ñö

instance safe' {p : ‚Ñï+} : FiniteDimensional ‚Ñö (CyclotomicField p ‚Ñö) :=
  IsCyclotomicExtension.finiteDimensional {p} ‚Ñö <| CyclotomicField p ‚Ñö

instance CyclotomicField.classGroupFinite {p : ‚Ñï+} :
    Fintype (ClassGroup <| ùìû (CyclotomicField p ‚Ñö)) :=
  ClassGroup.fintypeOfAdmissibleOfFinite ‚Ñö (CyclotomicField p ‚Ñö) AbsoluteValue.absIsAdmissible

end SafeInstances

variable (n p : ‚Ñï) [Fact p.Prime]

instance {p : ‚Ñï} [hp : Fact p.Prime] : Fact (0 < p) :=
  ‚ü®hp.out.pos‚ü©

-- note that this definition can be annoying to work with whilst #14984 isn't merged.
/-- A natural number `n` is regular if `n` is coprime with the cardinal of the class group -/
def IsRegularNumber [hn : Fact (0 < n)] : Prop :=
  n.coprime <| Fintype.card <| ClassGroup (ùìû <| CyclotomicField ‚ü®n, hn.out‚ü© ‚Ñö)

/-- The definition of regular primes. -/
def IsRegularPrime : Prop :=
  IsRegularNumber p

/-- A prime number is Bernoulli regular if it does not divide the numerator of any of
the first `p - 3` (non-zero) Bernoulli numbers-/
def IsBernoulliRegular : Prop :=
  ‚àÄ i ‚àà Finset.range ((p - 3) / 2), ((bernoulli 2 * i).num : ZMod p) ‚â† 0

/-- A prime is super regular if its regular and Bernoulli regular.-/
def IsSuperRegular : Prop :=
  IsRegularNumber p ‚àß IsBernoulliRegular p

section TwoRegular

variable (A K : Type _) [CommRing A] [IsDomain A] [Field K] [Algebra A K] [IsFractionRing A K]

variable (L : Type _) [Field L] [Algebra K L]

/-- The second cyclotomic field is equivalent to the base field. -/
def cyclotomicFieldTwoEquiv [IsCyclotomicExtension {2} K L] : L ‚âÉ‚Çê[K] K := by
  suffices IsSplittingField K K (cyclotomic 2 K) by
    letI : IsSplittingField K L (cyclotomic 2 K) :=
      IsCyclotomicExtension.splitting_field_cyclotomic 2 K L
    exact
      (IsSplittingField.algEquiv L (cyclotomic 2 K)).trans
        (IsSplittingField.algEquiv K <| cyclotomic 2 K).symm
  exact ‚ü®by simpa using @splits_X_sub_C _ _ _ _ (RingHom.id K) (-1), by simp‚ü©

instance (L : Type _) [Field L] [CharZero L] [IsCyclotomicExtension {2} ‚Ñö L] :
    IsPrincipalIdealRing (ùìû L) := by
  haveI : IsIntegralClosure ‚Ñ§ ‚Ñ§ L :=
    { algebraMap_injective' := (algebraMap ‚Ñ§ L).injective_int
      isIntegral_iff := fun {x} => by
        let f := cyclotomicFieldTwoEquiv ‚Ñö L
        refine'
          ‚ü®fun hx => ‚ü®IsIntegralClosure.mk' ‚Ñ§ (f x) (map_isIntegral_int f hx), f.injective _‚ü©, _‚ü©
        ¬∑ convert IsIntegralClosure.algebraMap_mk' ‚Ñ§ (f x) (map_isIntegral_int f hx)
          simp
        ¬∑ rintro ‚ü®y, hy‚ü©
          simpa [‚Üê hy] using isIntegral_algebraMap }
  let F : ùìû L ‚âÉ+* ‚Ñ§ := NumberField.RingOfIntegers.equiv _
  exact IsPrincipalIdealRing.of_surjective F.symm.toRingHom F.symm.surjective

set_option maxHeartbeats 1600000 in
set_option synthInstance.maxHeartbeats 400000 in
theorem isRegularNumber_two : IsRegularNumber 2 :=
  by
  rw [IsRegularNumber]
  convert coprime_one_right _
  dsimp
  apply (card_classGroup_eq_one_iff (R := ùìû (CyclotomicField (‚ü®2, two_pos‚ü© : ‚Ñï+) ‚Ñö))).2
  infer_instance

end TwoRegular
