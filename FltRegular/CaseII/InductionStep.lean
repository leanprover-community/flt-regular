module

public import FltRegular.CaseII.AuxLemmas
public import FltRegular.NumberTheory.KummersLemma.KummersLemma

@[expose] public section

open scoped nonZeroDivisors NumberField
open Polynomial

variable {K : Type} {p : â„•} [NeZero p] [Field K] (hp : p â‰  2)

variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p) {x y z : ğ“ K} {Îµ : (ğ“ K)Ë£}

local notation3 "Ï€" => Units.val (IsPrimitiveRoot.unit' hÎ¶) - 1
local notation3 "ğ”­" => Ideal.span {Ï€}
local notation3 "ğ”¦" Î· => Ideal.span {(x + y * Î· : ğ“ K)}
local notation3 "ğ”µ" => Ideal.span {x}
local notation3 "ğ”¶" => Ideal.span {y}
local notation3 "ğ”·" => Ideal.span {z}

variable {m : â„•} (e : x ^ p + y ^ p = Îµ * ((hÎ¶.unit'.1 - 1) ^ (m + 1) * z) ^ p)
variable (hy : Â¬ hÎ¶.unit'.1 - 1 âˆ£ y) (hz : Â¬ hÎ¶.unit'.1 - 1 âˆ£ z)
variable (Î· : nthRootsFinset p (1 : ğ“ K))

/- We have `x,y,z` elements of `O_K` and we assume that we have $$x^p+y^p= Îµ * ((Î¶-1)^(m+1)*z)^p$$-/

include e in
/- Let Ï€ = Î¶ -1, then Ï€ divides x^p+y^p. -/
lemma zeta_sub_one_dvd : Ï€ âˆ£ x ^ p + y ^ p := by
  rw [e, mul_pow, â† pow_mul]
  apply dvd_mul_of_dvd_right
  apply dvd_mul_of_dvd_left
  apply dvd_pow_self
  simp [NeZero.ne]

include e in
/- x^p+y^p = ğ”­^((m+1)*p) * (z)^p, here ğ”· = (z) (the ideal gen by z)-/
lemma span_pow_add_pow_eq :
    Ideal.span {x ^ p + y ^ p} = (ğ”­ ^ (m + 1) * ğ”·) ^ p := by
  simp only [e, â† Ideal.span_singleton_pow, â† Ideal.span_singleton_mul_span_singleton]
  convert one_mul _
  rw [Ideal.one_eq_top, Ideal.span_singleton_eq_top]
  exact Îµ.isUnit

variable [NumberField K]

local notation3 "ğ”ª" => gcd ğ”µ ğ”¶

include hy in
lemma m_ne_zero : ğ”ª â‰  0 := by
  simp_rw [Ne, gcd_eq_zero_iff, Ideal.zero_eq_bot, Ideal.span_singleton_eq_bot]
  rintro âŸ¨rfl, rflâŸ©
  exact hy (dvd_zero _)

variable [hpri : Fact p.Prime]

lemma coprime_c_aux (Î·â‚ Î·â‚‚ : nthRootsFinset p (1 : ğ“ K)) (hÎ· : Î·â‚ â‰  Î·â‚‚) :
    (ğ”¦ Î·â‚) âŠ” (ğ”¦ Î·â‚‚) âˆ£ ğ”ª * ğ”­ := by
  have : ğ”­ = Ideal.span (singleton <| (Î·â‚ : ğ“ K) - Î·â‚‚) := by
    rw [Ideal.span_singleton_eq_span_singleton]
    exact hÎ¶.unit'_coe.ntRootsFinset_pairwise_associated_sub_one_sub_of_prime hpri.out Î·â‚.prop
      Î·â‚‚.prop (Subtype.coe_injective.ne hÎ·)
  rw [(gcd_mul_right' ğ”­ ğ”µ ğ”¶).symm.dvd_iff_dvd_right, dvd_gcd_iff]
  simp_rw [this, Ideal.span_singleton_mul_span_singleton, Ideal.dvd_span_singleton,
    Ideal.mem_span_singleton_sup, Ideal.mem_span_singleton]
  refine âŸ¨âŸ¨-Î·â‚‚, _, âŸ¨Î·â‚, rflâŸ©, ?_âŸ©, âŸ¨1, _, âŸ¨-1, rflâŸ©, ?_âŸ©âŸ©
  Â· ring
  Â· ring

include hp hÎ¶ e hz in
lemma x_plus_y_mul_ne_zero : x + y * Î· â‰  0 := by
  intro hÎ·
  have : x + y * Î· âˆ£ x ^ p + y ^ p := by
    rw [hÎ¶.unit'_coe.pow_add_pow_eq_prod_add_mul _ _ <| Nat.odd_iff.2 <|
      hpri.out.eq_two_or_odd.resolve_left hp]
    simp_rw [mul_comm _ y]
    exact Finset.dvd_prod_of_mem _ Î·.prop
  rw [hÎ·, zero_dvd_iff, e] at this
  simp only [mul_eq_zero, Units.ne_zero, pow_eq_zero_iff (NeZero.ne p), false_or] at this
  rw [this.resolve_left (pow_ne_zero (m + 1) (hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt))] at hz
  exact hz (dvd_zero _)

variable [IsCyclotomicExtension {p} â„š K]

include e hp in
/- Let Ï€ = Î¶ -1, then Ï€ divides x+yÎ· with Î· a primivite root of unity. -/
lemma one_sub_zeta_dvd_zeta_pow_sub : Ï€ âˆ£ x + y * Î· := by
  letI : Fact (Nat.Prime p) := hpri
  letI := IsCyclotomicExtension.numberField {p} â„š K
  have h := zeta_sub_one_dvd hÎ¶ e
  replace h : âˆ _Î· âˆˆ nthRootsFinset p (1 : ğ“ K), Ideal.Quotient.mk ğ”­ (x + y * Î· : ğ“ K) = 0 := by
    rw [hÎ¶.unit'_coe.pow_add_pow_eq_prod_add_mul _ _ <| Nat.odd_iff.2 <|
      hpri.out.eq_two_or_odd.resolve_left hp, â† Ideal.Quotient.eq_zero_iff_dvd, map_prod] at h
    convert h using 2 with Î·' hÎ·'
    rw [map_add, map_add, map_mul, map_mul, IsPrimitiveRoot.eq_one_mod_one_sub' hÎ¶.unit'_coe hÎ·',
      IsPrimitiveRoot.eq_one_mod_one_sub' hÎ¶.unit'_coe Î·.prop, one_mul, mul_one]
  rw [Finset.prod_const, â† map_pow, Ideal.Quotient.eq_zero_iff_dvd] at h
  exact hÎ¶.zeta_sub_one_prime'.dvd_of_dvd_pow h

include hp hÎ¶ e in
/- x+yÎ· is divisible by Î¶-1 in O_k -/
lemma div_one_sub_zeta_mem : IsIntegral â„¤ ((x + y * Î· : ğ“ K) / (Î¶ - 1)) := by
  obtain âŸ¨âŸ¨a, haâŸ©, eâŸ© := one_sub_zeta_dvd_zeta_pow_sub hp hÎ¶ e Î·
  rw [e, mul_comm]
  simp only [map_mul, NumberField.RingOfIntegers.map_mk, map_sub,
    map_one, show hÎ¶.unit'.1 = Î¶ from rfl]
  rwa [mul_div_cancel_rightâ‚€ _ (hÎ¶.sub_one_ne_zero hpri.out.one_lt)]

/- Make (x+yÎ·)/(Î¶-1) into an element of O_K -/
def div_zeta_sub_one : nthRootsFinset p (1 : ğ“ K) â†’ ğ“ K :=
fun Î· â†¦ âŸ¨(x + y * Î·.1) / (Î¶ - 1), div_one_sub_zeta_mem hp hÎ¶ e Î·âŸ©

/-Check that if you multiply by Ï€ = Î¶ -1 you get back the original-/
lemma div_zeta_sub_one_mul_zeta_sub_one (Î·) :
    div_zeta_sub_one hp hÎ¶ e Î· * (Ï€) = x + y * Î· := by
  ext
  simp [show hÎ¶.unit'.1 = Î¶ from rfl,
    div_zeta_sub_one, div_mul_cancelâ‚€ _ (hÎ¶.sub_one_ne_zero hpri.out.one_lt)]

/- y is associated to (x+yÎ·â‚)/(Î¶-1) - (x+yÎ·â‚‚)/(Î¶-1) for Î·â‚ â‰  Î·â‚‚. -/
lemma div_zeta_sub_one_sub (Î·â‚ Î·â‚‚) (hÎ· : Î·â‚ â‰  Î·â‚‚) :
    Associated y (div_zeta_sub_one hp hÎ¶ e Î·â‚ - div_zeta_sub_one hp hÎ¶ e Î·â‚‚) := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  apply Associated.of_mul_right _ (Associated.refl (Ï€))
    (hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt)
  convert_to Associated _ (y * (Î·â‚ - Î·â‚‚))
  Â· rw [sub_mul, div_zeta_sub_one_mul_zeta_sub_one, div_zeta_sub_one_mul_zeta_sub_one]
    ring
  apply Associated.mul_left
  apply hÎ¶.unit'_coe.ntRootsFinset_pairwise_associated_sub_one_sub_of_prime hpri.out Î·â‚.prop Î·â‚‚.prop
  rw [Ne, â† Subtype.ext_iff.not]
  exact hÎ·

include hy in
/- sending Î· to (x+yÎ·)/(Î¶-1) mod (Ï€) = ğ”­ is injective. -/
lemma div_zeta_sub_one_Injective :
    Function.Injective (fun Î· â†¦ Ideal.Quotient.mk ğ”­ (div_zeta_sub_one hp hÎ¶ e Î·)) := by
  letI : AddGroup (ğ“ K â§¸ ğ”­) := inferInstance
  intros Î·â‚ Î·â‚‚
  contrapose
  intro eâ‚ eâ‚‚
  apply hy
  obtain âŸ¨u, eâŸ© := div_zeta_sub_one_sub hp hÎ¶ e Î·â‚ Î·â‚‚ eâ‚
  dsimp only at eâ‚‚
  rwa [â† sub_eq_zero, â† map_sub, â† e, Ideal.Quotient.eq_zero_iff_dvd, u.isUnit.dvd_mul_right] at eâ‚‚

/- quot by ideal is finite since we are in a number field.-/
instance : Finite (ğ“ K â§¸ ğ”­) := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rw [â† Ideal.absNorm_ne_zero_iff, Ne, Ideal.absNorm_eq_zero_iff, Ideal.span_singleton_eq_bot]
  exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

include hy in
/- sending Î· to (x+yÎ·)/(Î¶-1) mod (Ï€) = ğ”­ is bijective. -/
lemma div_zeta_sub_one_Bijective :
    Function.Bijective (fun Î· â†¦ Ideal.Quotient.mk ğ”­ (div_zeta_sub_one hp hÎ¶ e Î·)) := by
  letI := Fintype.ofFinite (ğ“ K â§¸ ğ”­)
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rw [Fintype.bijective_iff_injective_and_card]
  use div_zeta_sub_one_Injective hp hÎ¶ e hy
  simp only [Fintype.card_coe]
  rw [hÎ¶.unit'_coe.card_nthRootsFinset, â† Nat.card_eq_fintype_card, â† Submodule.cardQuot_apply,
    â† Ideal.absNorm_apply, Ideal.absNorm_span_singleton]
  simp [show Algebra.norm â„¤ Ï€ = _ from hÎ¶.norm_toInteger_sub_one_of_prime_ne_two' hp]

include hy in
/- the gcd of x y called ğ”ª is coprime to ğ”­-/
lemma gcd_zeta_sub_one_eq_one : gcd ğ”ª ğ”­ = 1 := by
  rw [gcd_assoc]
  convert gcd_one_right ğ”µ using 2
  rwa [gcd_comm, Irreducible.gcd_eq_one_iff, Ideal.dvd_span_singleton, Ideal.mem_span_singleton]
  Â· rw [irreducible_iff_prime]
    exact hÎ¶.prime_span_sub_one

include hy in
/- the ideal (x+yÎ·)/(Î¶ -1) is divisible by ğ”ª -/
lemma gcd_div_div_zeta_sub_one (Î·) : ğ”ª âˆ£ Ideal.span {div_zeta_sub_one hp hÎ¶ e Î·} := by
  rw [â† mul_one (Ideal.span {div_zeta_sub_one hp hÎ¶ e Î·}),
    â† gcd_zeta_sub_one_eq_one hÎ¶ hy (x := x) (y := y)]
  apply dvd_mul_gcd_of_dvd_mul
  rw [Ideal.span_singleton_mul_span_singleton, div_zeta_sub_one_mul_zeta_sub_one,
  Ideal.dvd_span_singleton, Ideal.gcd_eq_sup]
  refine add_mem
    (Ideal.mem_sup_left (Ideal.subset_span (s := {x}) rfl))
    (Ideal.mem_sup_right (Ideal.mul_mem_right _ _ (Ideal.subset_span (s := {y}) rfl)))

/- Give a name to ((x+yÎ·)/(Î¶ -1))/ğ”ª, call it ğ”  Î· -/
noncomputable
def div_zeta_sub_one_dvd_gcd : Ideal (ğ“ K) :=
(gcd_div_div_zeta_sub_one hp hÎ¶ e hy Î·).choose

local notation "ğ” " => fun Î· â†¦ div_zeta_sub_one_dvd_gcd hp hÎ¶ e hy Î·

lemma div_zeta_sub_one_dvd_gcd_spec :
    ğ”ª * ğ”  Î· = (Ideal.span <| singleton <| div_zeta_sub_one hp hÎ¶ e Î·) :=
(gcd_div_div_zeta_sub_one hp hÎ¶ e hy Î·).choose_spec.symm

lemma m_mul_c_mul_p : ğ”ª * ğ”  Î· * ğ”­ = ğ”¦ Î· := by
  rw [div_zeta_sub_one_dvd_gcd_spec, Ideal.span_singleton_mul_span_singleton,
    div_zeta_sub_one_mul_zeta_sub_one]

lemma p_ne_zero : ğ”­ â‰  0 := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rw [Ne, Ideal.zero_eq_bot, Ideal.span_singleton_eq_bot]
  exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

lemma coprime_c (Î·â‚ Î·â‚‚ : nthRootsFinset p (1 : ğ“ K)) (hÎ· : Î·â‚ â‰  Î·â‚‚) : IsCoprime (ğ”  Î·â‚) (ğ”  Î·â‚‚) := by
  rw [Ideal.isCoprime_iff_codisjoint, codisjoint_iff_le_sup, â† Ideal.dvd_iff_le]
  rw [â† mul_dvd_mul_iff_left (m_ne_zero hÎ¶ hy), â† mul_dvd_mul_iff_right (p_ne_zero hÎ¶)]
  rw [Ideal.mul_sup, Ideal.sup_mul, m_mul_c_mul_p, m_mul_c_mul_p, Ideal.mul_top]
  exact coprime_c_aux hÎ¶ Î·â‚ Î·â‚‚ hÎ·

include hy in
lemma gcd_m_p_pow_eq_one : gcd ğ”ª (ğ”­ ^ (m + 1)) = 1 := by
  rw [â† Ideal.isCoprime_iff_gcd, IsCoprime.pow_right_iff, Ideal.isCoprime_iff_gcd,
    gcd_zeta_sub_one_eq_one hÎ¶ hy]
  simp only [add_pos_iff, or_true, one_pos]

include hÎ¶ m hy e in
lemma m_dvd_z : ğ”ª âˆ£ ğ”· := by
  rw [â† one_mul ğ”·, â† gcd_m_p_pow_eq_one hÎ¶ hy (x := x) (m := m)]
  apply dvd_gcd_mul_of_dvd_mul
  rw [â† pow_dvd_pow_iff_dvd hpri.out.ne_zero, â† span_pow_add_pow_eq hÎ¶ e,
    Ideal.dvd_span_singleton]
  apply add_mem
  Â· exact Ideal.pow_mem_pow (Ideal.mem_sup_left (Ideal.mem_span_singleton_self x)) p
  Â· exact Ideal.pow_mem_pow (Ideal.mem_sup_right (Ideal.mem_span_singleton_self y)) p

noncomputable
def z_div_m : Ideal (ğ“ K) :=
(m_dvd_z hÎ¶ e hy).choose

local notation "ğ”·'" => z_div_m hÎ¶ e hy

lemma z_div_m_spec : ğ”· = ğ”ª * ğ”·' :=
  (m_dvd_z hÎ¶ e hy).choose_spec

lemma exists_ideal_pow_eq_c_aux :
    ğ”ª ^ p * (ğ”·' * ğ”­ ^ m) ^ p * ğ”­ ^ p = (ğ”­ ^ (m + 1) * ğ”·) ^ p := by
  rw [mul_comm _ ğ”·, mul_pow, z_div_m_spec hÎ¶ e hy, mul_pow, mul_pow, â† pow_mul, â† pow_mul,
    add_mul, one_mul, pow_add, mul_assoc, mul_assoc, mul_assoc]

/- The âˆ_Î·,  ğ”  Î· = (ğ”·' ğ”­^m)^p with ğ”· = ğ”ª ğ”·' -/
lemma prod_c : âˆ Î· âˆˆ Finset.attach (nthRootsFinset p (1 : ğ“ K)), ğ”  Î· = (ğ”·' * ğ”­ ^ m) ^ p := by
  have e' := span_pow_add_pow_eq hÎ¶ e
  rw [hÎ¶.unit'_coe.pow_add_pow_eq_prod_add_mul _ _ <| Nat.odd_iff.2 <|
    hpri.out.eq_two_or_odd.resolve_left hp] at e'
  rw [â† Ideal.prod_span_singleton, â† Finset.prod_attach] at e'
  simp_rw [mul_comm _ y, â† m_mul_c_mul_p hp hÎ¶ e hy,
    Finset.prod_mul_distrib, Finset.prod_const, Finset.card_attach,
    hÎ¶.unit'_coe.card_nthRootsFinset] at e'
  rw [â† mul_right_inj'
    ((pow_ne_zero_iff hpri.out.ne_zero).mpr (m_ne_zero hÎ¶ hy) : _),
    â† mul_left_inj' ((pow_ne_zero_iff hpri.out.ne_zero).mpr (p_ne_zero hÎ¶) : _), e',
    exists_ideal_pow_eq_c_aux]

/-each ğ”  Î· is a pth power, which will be denoted by ğ” Î· below. -/
lemma exists_ideal_pow_eq_c : âˆƒ I : Ideal (ğ“ K), (ğ”  Î·) = I ^ p :=
  Finset.exists_eq_pow_of_mul_eq_pow_of_coprime
    (fun Î·â‚ _ Î·â‚‚ _ hÎ· â†¦ coprime_c hp hÎ¶ e hy Î·â‚ Î·â‚‚ hÎ·)
    (prod_c hp hÎ¶ e hy) Î· (Finset.mem_attach _ _)

noncomputable
def root_div_zeta_sub_one_dvd_gcd : Ideal (ğ“ K) :=
  (exists_ideal_pow_eq_c hp hÎ¶ e hy Î·).choose

local notation "ğ”" => root_div_zeta_sub_one_dvd_gcd hp hÎ¶ e hy

lemma root_div_zeta_sub_one_dvd_gcd_spec : (ğ” Î·) ^ p = ğ”  Î· :=
(exists_ideal_pow_eq_c hp hÎ¶ e hy Î·).choose_spec.symm

/-x+yÎ·â‚ / (x+yÎ·â‚‚) = ğ”  Î·â‚/ ğ”  Î·â‚‚ -/
lemma c_div_principal_aux (Î·â‚ Î·â‚‚ : nthRootsFinset p (1 : ğ“ K)) :
    ((ğ”¦ Î·â‚) / (ğ”¦ Î·â‚‚) : FractionalIdeal (ğ“ K)â° K) = ğ”  Î·â‚ / ğ”  Î·â‚‚ := by
  simp_rw [â† m_mul_c_mul_p hp hÎ¶ e hy, FractionalIdeal.coeIdeal_mul]
  rw [mul_div_mul_right, mul_div_mul_left]
  Â· rw [â† FractionalIdeal.coeIdeal_bot, (FractionalIdeal.coeIdeal_injective' le_rfl).ne_iff]
    exact m_ne_zero hÎ¶ hy
  Â· rw [â† FractionalIdeal.coeIdeal_bot, (FractionalIdeal.coeIdeal_injective' le_rfl).ne_iff]
    exact p_ne_zero hÎ¶

lemma c_div_principal (Î·â‚ Î·â‚‚ : nthRootsFinset p (1 : ğ“ K)) :
    Submodule.IsPrincipal ((ğ”  Î·â‚ / ğ”  Î·â‚‚ : FractionalIdeal (ğ“ K)â° K) : Submodule (ğ“ K) K) := by
  rw [â† c_div_principal_aux, FractionalIdeal.coeIdeal_span_singleton,
    FractionalIdeal.coeIdeal_span_singleton, FractionalIdeal.spanSingleton_div_spanSingleton,
    FractionalIdeal.coe_spanSingleton]
  exact âŸ¨âŸ¨_, rflâŸ©âŸ©

noncomputable
def zeta_sub_one_dvd_root : nthRootsFinset p (1 : ğ“ K) :=
(Equiv.ofBijective _ (div_zeta_sub_one_Bijective hp hÎ¶ e hy)).symm 0

/- This is the Î·â‚€ such that (x+yÎ·â‚€)/(Î¶-1) is zero mod ğ”­. which is unique-/
local notation "Î·â‚€" => zeta_sub_one_dvd_root hp hÎ¶ e hy

lemma zeta_sub_one_dvd_root_spec : Ideal.Quotient.mk ğ”­ (div_zeta_sub_one hp hÎ¶ e Î·â‚€) = 0 :=
Equiv.ofBijective_apply_symm_apply _ (div_zeta_sub_one_Bijective hp hÎ¶ e hy) 0

lemma p_dvd_c_iff : ğ”­ âˆ£ (ğ”  Î·) â†” Î· = Î·â‚€ := by
  rw [â† (div_zeta_sub_one_Injective hp hÎ¶ e hy).eq_iff, zeta_sub_one_dvd_root_spec,
    Ideal.Quotient.eq_zero_iff_dvd, â† Ideal.mem_span_singleton (Î± := ğ“ K),
    â† Ideal.dvd_span_singleton, â† div_zeta_sub_one_dvd_gcd_spec (hy := hy),
    â† dvd_gcd_mul_iff_dvd_mul, gcd_comm, gcd_zeta_sub_one_eq_one hÎ¶ hy, one_mul]

/- All the others ğ”  Î· are coprime to ğ”­...basically-/
lemma p_pow_dvd_c_eta_zero_aux [DecidableEq (ğ“ K)] :
  gcd (ğ”­ ^ (m * p)) (âˆ Î· âˆˆ Finset.attach (nthRootsFinset p (1 : ğ“ K)) \ {Î·â‚€}, ğ”  Î·) = 1 := by
    rw [â† Ideal.isCoprime_iff_gcd]
    apply IsCoprime.pow_left
    rw [Ideal.isCoprime_iff_gcd, hÎ¶.prime_span_sub_one.irreducible.gcd_eq_one_iff,
      hÎ¶.prime_span_sub_one.dvd_finset_prod_iff]
    rintro âŸ¨Î·, hÎ·, hâŸ©
    rw [p_dvd_c_iff] at h
    simp only [Finset.mem_sdiff, Finset.mem_singleton] at hÎ·
    exact hÎ·.2 h

lemma p_dvd_a_iff : ğ”­ âˆ£ ğ” Î· â†” Î· = Î·â‚€ := by
  rw [â† p_dvd_c_iff hp hÎ¶ e hy, â† root_div_zeta_sub_one_dvd_gcd_spec,
    hÎ¶.prime_span_sub_one.dvd_pow_iff_dvd hpri.out.ne_zero]

/- all the powers of ğ”­ have to be in ğ”  Î·â‚€-/
lemma p_pow_dvd_c_eta_zero : ğ”­ ^ (m * p) âˆ£ ğ”  Î·â‚€ := by
  classical
  rw [â† one_mul (ğ”  Î·â‚€), â† p_pow_dvd_c_eta_zero_aux hp hÎ¶ e hy, dvd_gcd_mul_iff_dvd_mul,
    mul_comm _ (ğ”  Î·â‚€), â† Finset.prod_eq_mul_prod_diff_singleton (Finset.mem_attach _ Î·â‚€) ğ” ,
    prod_c, mul_pow]
  apply dvd_mul_of_dvd_right
  rw [pow_mul]

/- since the is only one ğ” Î· which is divisble by ğ”­ it has to be the Î·â‚€ one and it has to divide
to ğ”­^m power.-/
lemma p_pow_dvd_a_eta_zero : ğ”­ ^ m âˆ£ ğ” Î·â‚€ := by
  rw [â† pow_dvd_pow_iff_dvd hpri.out.ne_zero, root_div_zeta_sub_one_dvd_gcd_spec, â† pow_mul]
  exact p_pow_dvd_c_eta_zero hp hÎ¶ e hy

noncomputable
def a_eta_zero_dvd_p_pow : Ideal (ğ“ K) :=
  (p_pow_dvd_a_eta_zero hp hÎ¶ e hy).choose

/-ğ”â‚€ is the coprime to ğ”­ bit of ğ” Î·â‚€-/
local notation "ğ”â‚€" => a_eta_zero_dvd_p_pow hp hÎ¶ e hy

lemma a_eta_zero_dvd_p_pow_spec : ğ”­ ^ m * ğ”â‚€ = ğ” Î·â‚€ :=
  (p_pow_dvd_a_eta_zero hp hÎ¶ e hy).choose_spec.symm

include hz in
lemma not_p_div_a_zero : Â¬ ğ”­ âˆ£ ğ”â‚€ := by
  intro h
  have := pow_dvd_pow_of_dvd (mul_dvd_mul (dvd_refl (ğ”­ ^ m)) h) p
  rw [a_eta_zero_dvd_p_pow_spec, root_div_zeta_sub_one_dvd_gcd_spec] at this
  have := this.trans (Finset.dvd_prod_of_mem ğ”  (Finset.mem_attach _ Î·â‚€))
  rw [prod_c, mul_pow, mul_pow, mul_comm, mul_dvd_mul_iff_right,
    pow_dvd_pow_iff_dvd hpri.out.ne_zero] at this
  Â· apply hz
    rw [â† Ideal.mem_span_singleton, â† Ideal.dvd_span_singleton, z_div_m_spec hÎ¶ e hy]
    exact this.trans (dvd_mul_left _ _)
  Â· apply mt eq_zero_of_pow_eq_zero
    apply mt eq_zero_of_pow_eq_zero
    rw [Ideal.zero_eq_bot, Ideal.span_singleton_eq_bot]
    exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

include hp hÎ¶ e hy hz in
lemma one_le_m : 1 â‰¤ m := by
  have ha := not_p_div_a_zero hp hÎ¶ e hy hz
  rw [â† hÎ¶.prime_span_sub_one.irreducible.gcd_eq_one_iff] at ha
  have := (p_dvd_a_iff hp hÎ¶ e hy Î·â‚€).mpr rfl
  rw [â† a_eta_zero_dvd_p_pow_spec, mul_comm, â† dvd_gcd_mul_iff_dvd_mul, ha, one_mul] at this
  nth_rw 1 [â† pow_one ğ”­] at this
  rwa [â† pow_dvd_pow_iff (p_ne_zero hÎ¶) hÎ¶.prime_span_sub_one.not_unit]

include hp in
lemma exists_solution'_aux {Îµâ‚ Îµâ‚‚ : (ğ“ K)Ë£} (hx : Â¬ Ï€ âˆ£ x)
    (h : (p : ğ“ K) âˆ£ Îµâ‚ * x ^ p + Îµâ‚‚ * y ^ p) :
    âˆƒ a : ğ“ K, â†‘p âˆ£ â†‘(Îµâ‚ / Îµâ‚‚) - a ^ p := by
  obtain âŸ¨a, b, eâŸ© : IsCoprime â†‘p x := isCoprime_of_not_zeta_sub_one_dvd hÎ¶ hx
  have : (p : ğ“ K) âˆ£ b * x - 1 := by use -a; rw [â† e]; ring
  have := (this.trans (sub_one_dvd_pow_sub_one _ p)).trans (dvd_mul_left _ â†‘(Îµâ‚ / Îµâ‚‚))
  use - y * b
  replace h := (h.trans (dvd_mul_right _ (b ^ p))).trans (dvd_mul_left _ â†‘(Îµâ‚‚â»Â¹))
  rw [add_mul, mul_assoc, mul_assoc, â† mul_pow, â† mul_pow, mul_add] at h
  simp_rw [â† mul_assoc, â† Units.val_mul] at h
  rw [â† mul_comm Îµâ‚, â† div_eq_mul_inv, inv_mul_cancel, Units.val_one, one_mul] at h
  convert dvd_sub h this using 1
  rw [neg_mul, (Nat.Prime.odd_of_ne_two hpri.out hp).neg_pow, sub_neg_eq_add, mul_sub, mul_one,
    mul_comm x b, add_sub_sub_cancel, add_comm]

variable [Fintype (ClassGroup (ğ“ K))] (hreg : p.Coprime <| Fintype.card <| ClassGroup (ğ“ K))

include hreg in
lemma a_div_principal (Î·â‚ Î·â‚‚ : nthRootsFinset p (1 : ğ“ K)) :
    Submodule.IsPrincipal ((ğ” Î·â‚ / ğ” Î·â‚‚ : FractionalIdeal (ğ“ K)â° K) : Submodule (ğ“ K) K) := by
  apply isPrincipal_of_isPrincipal_pow_of_Coprime' _ hreg
  /- the line above is where we use the p is regular.-/
  rw [div_pow, â† FractionalIdeal.coeIdeal_pow, â† FractionalIdeal.coeIdeal_pow,
    root_div_zeta_sub_one_dvd_gcd_spec, root_div_zeta_sub_one_dvd_gcd_spec]
  exact c_div_principal hp hÎ¶ e hy Î·â‚ Î·â‚‚

include hreg in
lemma isPrincipal_a_div_a_zero :
    Submodule.IsPrincipal ((ğ” Î· / ğ”â‚€ : FractionalIdeal (ğ“ K)â° K) : Submodule (ğ“ K) K) := by
  have := a_div_principal hp hÎ¶ e hy hreg Î· Î·â‚€
  rw [â† a_eta_zero_dvd_p_pow_spec, mul_comm, FractionalIdeal.coeIdeal_mul, â† div_div,
   FractionalIdeal.isPrincipal_iff] at this
  obtain âŸ¨a, haâŸ© := this
  rw [div_eq_iff, Ideal.span_singleton_pow, FractionalIdeal.coeIdeal_span_singleton,
    FractionalIdeal.spanSingleton_mul_spanSingleton] at ha
  Â· rw [FractionalIdeal.isPrincipal_iff]
    exact âŸ¨_, haâŸ©
  Â· rw [â† FractionalIdeal.coeIdeal_bot,
      (FractionalIdeal.coeIdeal_injective' (le_rfl : (ğ“ K)â° â‰¤ (ğ“ K)â°)).ne_iff]
    apply mt eq_zero_of_pow_eq_zero
    rw [Ideal.zero_eq_bot, Ideal.span_singleton_eq_bot]
    exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

include hz hreg in
lemma exists_not_dvd_spanSingleton_eq_a_div_a_zero (hÎ· : Î· â‰  Î·â‚€) :
    âˆƒ a b : ğ“ K, Â¬ Ï€ âˆ£ a âˆ§ Â¬ Ï€ âˆ£ b âˆ§
      FractionalIdeal.spanSingleton (ğ“ K)â° (a / b : K) = ğ” Î· / ğ”â‚€ := by
  exact exists_not_dvd_spanSingleton_eq hÎ¶.zeta_sub_one_prime'
    _ _ ((p_dvd_a_iff hp hÎ¶ e hy Î·).not.mpr hÎ·) (not_p_div_a_zero hp hÎ¶ e hy hz)
      (isPrincipal_a_div_a_zero hp hÎ¶ e hy Î· hreg)

noncomputable
def a_div_a_zero_num (hÎ· : Î· â‰  Î·â‚€) : ğ“ K :=
  (exists_not_dvd_spanSingleton_eq_a_div_a_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose

noncomputable
def a_div_a_zero_denom (hÎ· : Î· â‰  Î·â‚€) : ğ“ K :=
  (exists_not_dvd_spanSingleton_eq_a_div_a_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose_spec.choose

local notation "Î±" => fun Î· â†¦ a_div_a_zero_num hp hÎ¶ e hy hz Î· hreg
local notation "Î²" => fun Î· â†¦ a_div_a_zero_denom hp hÎ¶ e hy hz Î· hreg

include hreg in
lemma a_div_a_zero_num_spec (hÎ· : Î· â‰  Î·â‚€) : Â¬ Ï€ âˆ£ Î± Î· hÎ· :=
  (exists_not_dvd_spanSingleton_eq_a_div_a_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose_spec.choose_spec.1

include hreg in
lemma a_div_a_zero_denom_spec (hÎ· : Î· â‰  Î·â‚€) : Â¬ Ï€ âˆ£ Î² Î· hÎ· :=
  (exists_not_dvd_spanSingleton_eq_a_div_a_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose_spec.choose_spec.2.1

/- eqn 7.8 of Borevich-Shafarevich-/
lemma a_div_a_zero_eq (hÎ· : Î· â‰  Î·â‚€) :
    FractionalIdeal.spanSingleton (ğ“ K)â° (Î± Î· hÎ· / Î² Î· hÎ· : K) = ğ” Î· / ğ”â‚€ :=
  (exists_not_dvd_spanSingleton_eq_a_div_a_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose_spec.choose_spec.2.2

lemma a_mul_denom_eq_a_zero_mul_num (hÎ· : Î· â‰  Î·â‚€) :
    ğ” Î· * Ideal.span {Î² Î· hÎ·} = ğ”â‚€ * Ideal.span {Î± Î· hÎ·} := by
  apply FractionalIdeal.coeIdeal_injective (K := K)
  simp only [FractionalIdeal.coeIdeal_mul, FractionalIdeal.coeIdeal_span_singleton]
  rw [mul_comm (ğ”â‚€ : FractionalIdeal (ğ“ K)â° K), â† div_eq_div_iff,
    â† a_div_a_zero_eq hp hÎ¶ e hy hz Î· hreg hÎ·, FractionalIdeal.spanSingleton_div_spanSingleton]
  Â· intro ha
    rw [FractionalIdeal.coeIdeal_eq_zero] at ha
    apply not_p_div_a_zero hp hÎ¶ e hy hz
    rw [ha]
    exact dvd_zero _
  Â· rw [Ne, FractionalIdeal.spanSingleton_eq_zero_iff, â† (algebraMap (ğ“ K) K).map_zero,
      (IsFractionRing.injective (ğ“ K) K).eq_iff]
    intro hÎ²
    apply a_div_a_zero_denom_spec hp hÎ¶ e hy hz Î· hreg hÎ·
    simp only
    rw [hÎ²]
    exact dvd_zero _

/- eqn 7.9 of BS -/
lemma associated_eta_zero (hÎ· : Î· â‰  Î·â‚€) :
    Associated ((x + y * Î·â‚€) * Î± Î· hÎ· ^ p)
      ((x + y * Î·) * Ï€ ^ (m * p) * Î² Î· hÎ· ^ p) := by
  simp_rw [â† Ideal.span_singleton_eq_span_singleton,
    â† Ideal.span_singleton_mul_span_singleton, â† Ideal.span_singleton_pow,
    â† m_mul_c_mul_p hp hÎ¶ e hy, â† root_div_zeta_sub_one_dvd_gcd_spec, â† a_eta_zero_dvd_p_pow_spec]
  rw [mul_comm _ ğ”â‚€, mul_pow]
  simp only [mul_assoc, mul_left_comm _ ğ”­]
  rw [mul_left_comm (ğ” Î· ^ p), mul_left_comm (ğ”â‚€ ^ p), â† pow_mul, â† mul_pow, â† mul_pow,
    a_mul_denom_eq_a_zero_mul_num]

noncomputable
def associated_eta_zero_unit (hÎ· : Î· â‰  Î·â‚€) : (ğ“ K)Ë£ :=
(associated_eta_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose

local notation "Îµ" => fun Î· â†¦ associated_eta_zero_unit hp hÎ¶ e hy hz Î· hreg

lemma associated_eta_zero_unit_spec (Î·) (hÎ· : Î· â‰  Î·â‚€) :
    Îµ Î· hÎ· * (x + y * Î·â‚€) * Î± Î· hÎ· ^ p = (x + y * Î·) * Ï€ ^ (m * p) * Î² Î· hÎ· ^ p := by
  rw [mul_assoc, mul_comm (Îµ Î· hÎ· : ğ“ K)]
  exact (associated_eta_zero hp hÎ¶ e hy hz Î· hreg hÎ·).choose_spec

lemma formula (Î·â‚) (hÎ·â‚ : Î·â‚ â‰  Î·â‚€) (Î·â‚‚) (hÎ·â‚‚ : Î·â‚‚ â‰  Î·â‚€) :
  (Î·â‚‚ - Î·â‚€ : ğ“ K) * Îµ Î·â‚ hÎ·â‚ * (Î± Î·â‚ hÎ·â‚ * Î² Î·â‚‚ hÎ·â‚‚) ^ p +
    (Î·â‚€ - Î·â‚) * Îµ Î·â‚‚ hÎ·â‚‚ * (Î± Î·â‚‚ hÎ·â‚‚ * Î² Î·â‚ hÎ·â‚) ^ p =
    (Î·â‚‚ - Î·â‚) * (Ï€ ^ m * (Î² Î·â‚ hÎ·â‚ * Î² Î·â‚‚ hÎ·â‚‚)) ^ p := by
  rw [â† mul_right_inj' (x_plus_y_mul_ne_zero hp hÎ¶ e hz Î·â‚€), mul_add]
  simp_rw [mul_left_comm (x + y * Î·â‚€), mul_pow, mul_assoc, mul_left_comm (Î·â‚‚ - Î·â‚€ : ğ“ K),
    mul_left_comm (Î·â‚€ - Î·â‚ : ğ“ K), â† mul_assoc,
    associated_eta_zero_unit_spec, mul_assoc, â† mul_left_comm (Î·â‚‚ - Î·â‚€ : ğ“ K),
    â† mul_left_comm (Î·â‚€ - Î·â‚ : ğ“ K), pow_mul, â† mul_pow, mul_comm (Î² Î·â‚‚ hÎ·â‚‚), â† mul_assoc]
  rw [â† add_mul]
  congr 1
  ring

include hreg e hy hz hp in
lemma exists_solution :
    âˆƒ (x' y' z' : ğ“ K) (Îµâ‚ Îµâ‚‚ Îµâ‚ƒ : (ğ“ K)Ë£), Â¬ Ï€ âˆ£ x' âˆ§ Â¬ Ï€ âˆ£ y' âˆ§ Â¬ Ï€ âˆ£ z' âˆ§
      Îµâ‚ * x' ^ p + Îµâ‚‚ * y' ^ p = Îµâ‚ƒ * (Ï€ ^ m * z') ^ p := by
  have hâ‚ := mul_mem_nthRootsFinset (Î·â‚€ : _).prop (hÎ¶.unit'_coe.mem_nthRootsFinset hpri.out.pos)
  rw [one_mul] at hâ‚
  let Î·â‚ : nthRootsFinset p (1 : ğ“ K) := âŸ¨Î·â‚€ * hÎ¶.unit', hâ‚âŸ©
  have hâ‚‚ := mul_mem_nthRootsFinset (Î·â‚ : _).prop (hÎ¶.unit'_coe.mem_nthRootsFinset hpri.out.pos)
  rw [one_mul] at hâ‚‚
  let Î·â‚‚ : nthRootsFinset p (1 : ğ“ K) := âŸ¨Î·â‚€ * hÎ¶.unit' * hÎ¶.unit', hâ‚‚âŸ©
  have hÎ·â‚ : Î·â‚ â‰  Î·â‚€ := by
    rw [â† Subtype.coe_injective.ne_iff]
    change (Î·â‚€ * hÎ¶.unit' : ğ“ K) â‰  Î·â‚€
    rw [Ne, mul_right_eq_selfâ‚€, not_or]
    exact âŸ¨hÎ¶.unit'_coe.ne_one hpri.out.one_lt,
      ne_zero_of_mem_nthRootsFinset one_ne_zero (Î·â‚€ : _).propâŸ©
  have hÎ·â‚‚ : Î·â‚‚ â‰  Î·â‚€ := by
    rw [â† Subtype.coe_injective.ne_iff]
    change (Î·â‚€ * hÎ¶.unit' * hÎ¶.unit' : ğ“ K) â‰  Î·â‚€
    rw [Ne, mul_assoc, â† pow_two, mul_right_eq_selfâ‚€, not_or]
    exact âŸ¨hÎ¶.unit'_coe.pow_ne_one_of_pos_of_lt (by omega)
      (hpri.out.two_le.lt_or_eq.resolve_right hp.symm),
      ne_zero_of_mem_nthRootsFinset one_ne_zero (Î·â‚€ : _).propâŸ©
  have hÎ· : Î·â‚‚ â‰  Î·â‚ := by
    rw [â† Subtype.coe_injective.ne_iff]
    change (Î·â‚€ * hÎ¶.unit' * hÎ¶.unit' : ğ“ K) â‰  Î·â‚€ * hÎ¶.unit'
    rw [Ne, mul_right_eq_selfâ‚€, not_or]
    exact âŸ¨hÎ¶.unit'_coe.ne_one hpri.out.one_lt,
      mul_ne_zero (ne_zero_of_mem_nthRootsFinset one_ne_zero (Î·â‚€ : _).prop)
      (hÎ¶.unit'_coe.ne_zero hpri.out.ne_zero)âŸ©
  obtain âŸ¨uâ‚, huâ‚âŸ© := hÎ¶.unit'_coe.ntRootsFinset_pairwise_associated_sub_one_sub_of_prime hpri.out
    Î·â‚‚.prop (Î·â‚€ : _).prop (Subtype.coe_injective.ne_iff.mpr hÎ·â‚‚)
  obtain âŸ¨uâ‚‚, huâ‚‚âŸ© := hÎ¶.unit'_coe.ntRootsFinset_pairwise_associated_sub_one_sub_of_prime hpri.out
    (Î·â‚€ : _).prop Î·â‚.prop (Subtype.coe_injective.ne_iff.mpr hÎ·â‚.symm)
  obtain âŸ¨uâ‚ƒ, huâ‚ƒâŸ© := hÎ¶.unit'_coe.ntRootsFinset_pairwise_associated_sub_one_sub_of_prime hpri.out
    Î·â‚‚.prop (Î·â‚ : _).prop (Subtype.coe_injective.ne_iff.mpr hÎ·)
  have := formula hp hÎ¶ e hy hz hreg Î·â‚ hÎ·â‚ Î·â‚‚ hÎ·â‚‚
  rw [â† huâ‚, â† huâ‚‚, â† huâ‚ƒ, mul_assoc _ (uâ‚ : ğ“ K), mul_assoc _ (uâ‚‚ : ğ“ K), mul_assoc _ (uâ‚ƒ : ğ“ K),
    mul_assoc (Ï€), mul_assoc (Ï€), â† mul_add,
    mul_right_inj' (hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt), â† Units.val_mul,
    â† Units.val_mul] at this
  refine âŸ¨_, _, _, _, _, _, ?_, ?_, ?_, thisâŸ©
  Â· exact hÎ¶.zeta_sub_one_prime'.not_dvd_mul
      (a_div_a_zero_num_spec hp hÎ¶ e hy hz Î·â‚ hreg hÎ·â‚)
      (a_div_a_zero_denom_spec hp hÎ¶ e hy hz Î·â‚‚ hreg hÎ·â‚‚)
  Â· exact hÎ¶.zeta_sub_one_prime'.not_dvd_mul
      (a_div_a_zero_num_spec hp hÎ¶ e hy hz Î·â‚‚ hreg hÎ·â‚‚)
      (a_div_a_zero_denom_spec hp hÎ¶ e hy hz Î·â‚ hreg hÎ·â‚)
  Â· exact hÎ¶.zeta_sub_one_prime'.not_dvd_mul
      (a_div_a_zero_denom_spec hp hÎ¶ e hy hz Î·â‚ hreg hÎ·â‚)
      (a_div_a_zero_denom_spec hp hÎ¶ e hy hz Î·â‚‚ hreg hÎ·â‚‚)

include hp hreg e hy hz in
lemma exists_solution' :
    âˆƒ (x' y' z' : ğ“ K) (Îµâ‚ƒ : (ğ“ K)Ë£),
      Â¬ Ï€ âˆ£ y' âˆ§ Â¬ Ï€ âˆ£ z' âˆ§ x' ^ p + y' ^ p = Îµâ‚ƒ * (Ï€ ^ m * z') ^ p := by
  obtain âŸ¨x', y', z', Îµâ‚, Îµâ‚‚, Îµâ‚ƒ, hx', hy', hz', e'âŸ© := exists_solution hp hÎ¶ e hy hz hreg
  obtain âŸ¨Îµ', hÎµ'âŸ© : âˆƒ Îµ', Îµâ‚ / Îµâ‚‚ = Îµ' ^ p := by
    apply eq_pow_prime_of_unit_of_congruent hp hreg --this is Kummers
    have : p - 1 â‰¤ m * p := (Nat.sub_le _ _).trans
      ((le_of_eq (one_mul _).symm).trans (Nat.mul_le_mul_right p (one_le_m hp hÎ¶ e hy hz)))
    obtain âŸ¨u, huâŸ© := (associated_zeta_sub_one_pow_prime hÎ¶).symm
    rw [mul_pow, â† pow_mul, mul_comm (Îµâ‚ƒ : ğ“ K), mul_assoc, â† Nat.sub_add_cancel this,
      add_comm _ (p - 1), pow_add, mul_assoc] at e'
    apply_fun Ideal.Quotient.mk (Ideal.span <| singleton (p : ğ“ K)) at e'
    rw [map_mul, (Ideal.Quotient.eq_zero_iff_dvd _ _).mpr
      (associated_zeta_sub_one_pow_prime hÎ¶).symm.dvd, zero_mul,
      Ideal.Quotient.eq_zero_iff_dvd] at e'
    obtain âŸ¨a, haâŸ© := exists_solution'_aux hp hÎ¶ hx' e'
    obtain âŸ¨b, hbâŸ© := exists_dvd_pow_sub_Int_pow hp a
    have := dvd_add ha hb
    rw [sub_add_sub_cancel, â† Int.cast_pow] at this
    exact âŸ¨b ^ p, thisâŸ©
  refine âŸ¨Îµ' * x', y', z', Îµâ‚ƒ / Îµâ‚‚, hy', hz', ?_âŸ©
  rwa [mul_pow, â† Units.val_pow_eq_pow_val, â† hÎµ', â† mul_right_inj' Îµâ‚‚.isUnit.ne_zero,
    mul_add, â† mul_assoc, â† Units.val_mul, mul_div_cancel,
    â† mul_assoc, â† Units.val_mul, mul_div_cancel]
