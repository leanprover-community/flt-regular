import FltRegular.CaseII.InductionStep

open scoped BigOperators nonZeroDivisors NumberField
open Polynomial

variable {K : Type*} {p : ‚Ñï} [hpri : Fact p.Prime] [Field K] [NumberField K]
  [IsCyclotomicExtension {p} ‚Ñö K]  (hp : p ‚â† 2) [Fintype (ClassGroup (ùìû K))]
  (hreg : p.Coprime <| Fintype.card <| ClassGroup (ùìû K))

variable {Œ∂ : K} (hŒ∂ : IsPrimitiveRoot Œ∂ p)

namespace FltRegular

include hp hreg in
lemma not_exists_solution {m : ‚Ñï} (hm : 1 ‚â§ m) :
  ¬¨‚àÉ (x' y' z' : ùìû K) (Œµ‚ÇÉ : (ùìû K)À£),
    ¬¨((hŒ∂.unit' : ùìû K) - 1 ‚à£ y') ‚àß ¬¨((hŒ∂.unit' : ùìû K) - 1 ‚à£ z') ‚àß
    x' ^ p + y' ^ p = Œµ‚ÇÉ * (((hŒ∂.unit' : ùìû K) - 1) ^ m * z') ^ p := by
  induction' m, hm using Nat.le_induction with m' _ IH
  ¬∑ rintro ‚ü®x, y, z, Œµ‚ÇÉ, hy, hz, e‚ü©
    exact zero_lt_one.not_ge (one_le_m hp hŒ∂ e hy hz)
  ¬∑ rintro ‚ü®x, y, z, Œµ‚ÇÉ, hy, hz, e‚ü©
    exact IH (exists_solution' hp hŒ∂ e hy hz hreg)

include hp hreg in
lemma not_exists_solution' :
  ¬¨‚àÉ (x y z : ùìû K), ¬¨(hŒ∂.unit' : ùìû K) - 1 ‚à£ y ‚àß (hŒ∂.unit' : ùìû K) - 1 ‚à£ z ‚àß z ‚â† 0 ‚àß
    x ^ p + y ^ p = z ^ p := by
  letI : Fact (Nat.Prime p) := hpri
  letI : WfDvdMonoid (ùìû K) := IsNoetherianRing.wfDvdMonoid
  rintro ‚ü®x, y, z, hy, hz, hz', e‚ü©
  obtain ‚ü®m, z, hm, hz'', rfl‚ü© :
    ‚àÉ m z', 1 ‚â§ m ‚àß ¬¨((hŒ∂.unit' : ùìû K) - 1 ‚à£ z') ‚àß z = ((hŒ∂.unit' : ùìû K) - 1) ^ m * z' := by
    classical
    have H : FiniteMultiplicity ((hŒ∂.unit' : ùìû K) - 1) z := FiniteMultiplicity.of_not_isUnit
      hŒ∂.zeta_sub_one_prime'.not_unit hz'
    obtain ‚ü®z', h‚ü© := pow_multiplicity_dvd ((hŒ∂.unit' : ùìû K) - 1) z
    refine ‚ü®_, _, ?_, ?_, h‚ü©
    ¬∑ rwa [‚Üê Nat.cast_le (Œ± := ENat), ‚Üê FiniteMultiplicity.emultiplicity_eq_multiplicity H,
        ‚Üê pow_dvd_iff_le_emultiplicity, pow_one]
    ¬∑ intro h'
      have := mul_dvd_mul_left (((hŒ∂.unit' : ùìû K) - 1) ^ (multiplicity ((hŒ∂.unit' : ùìû K) - 1) z)) h'
      rw [‚Üê pow_succ, ‚Üê h] at this
      refine not_pow_dvd_of_emultiplicity_lt ?_ this
      rw [FiniteMultiplicity.emultiplicity_eq_multiplicity H, Nat.cast_lt]
      exact Nat.lt_succ_self _
  refine not_exists_solution hp hreg hŒ∂ hm ‚ü®x, y, z, 1, hy, hz'', ?_‚ü©
  rwa [Units.val_one, one_mul]

lemma not_exists_Int_solution {p : ‚Ñï} [hpri : Fact (Nat.Prime p)] (hreg : IsRegularPrime p)
    (hodd : p ‚â† 2) : ¬¨‚àÉ (x y z : ‚Ñ§), ¬¨‚Üëp ‚à£ y ‚àß ‚Üëp ‚à£ z ‚àß z ‚â† 0 ‚àß x ^ p + y ^ p = z ^ p := by
  haveI := CyclotomicField.isCyclotomicExtension p ‚Ñö
  obtain ‚ü®Œ∂, hŒ∂‚ü© := IsCyclotomicExtension.exists_isPrimitiveRoot
    ‚Ñö (B := (CyclotomicField p ‚Ñö)) (Set.mem_singleton p) hpri.1.ne_zero
  have := fun n ‚Ü¶ zeta_sub_one_dvd_Int_iff (K := CyclotomicField p ‚Ñö) hŒ∂ (n := n)
  simp only [PNat.mk_coe] at this
  simp_rw [‚Üê this]
  rintro ‚ü®x, y, z, hy, hz, hz', e‚ü©
  refine not_exists_solution' (K := CyclotomicField p ‚Ñö) hodd ?_
    hŒ∂ ‚ü®x, y, z, hy, hz, ?_, ?_‚ü©
  ¬∑ convert hreg
  ¬∑ rwa [ne_eq, Int.cast_eq_zero]
  ¬∑ dsimp
    simp_rw [‚Üê Int.cast_pow, ‚Üê Int.cast_add, e]

lemma not_exists_Int_solution' {p : ‚Ñï} [hpri : Fact (Nat.Prime p)] (hreg : IsRegularPrime p)
    (hodd : p ‚â† 2) :
    ¬¨‚àÉ (x y z : ‚Ñ§), ({x, y, z} : Finset ‚Ñ§).gcd id = 1 ‚àß ‚Üëp ‚à£ z ‚àß z ‚â† 0 ‚àß x ^ p + y ^ p = z ^ p := by
  rintro ‚ü®x, y, z, hgcd, hz, hz', e‚ü©
  refine not_exists_Int_solution hreg hodd ‚ü®x, y, z, ?_, hz, hz', e‚ü©
  intro hy
  have := dvd_sub (dvd_pow hz hpri.out.ne_zero) (dvd_pow hy hpri.out.ne_zero)
  rw [‚Üê e, add_sub_cancel_right] at this
  replace this := (Nat.prime_iff_prime_int.mp hpri.out).dvd_of_dvd_pow this
  apply (Nat.prime_iff_prime_int.mp hpri.out).not_unit
  rw [isUnit_iff_dvd_one, ‚Üê hgcd]
  simp [dvd_gcd_iff, hz, hy, this]

lemma Int.gcd_left_comm (a b c : ‚Ñ§) : Int.gcd a (Int.gcd b c) = Int.gcd b (Int.gcd a c) := by
  rw [‚Üê Int.gcd_assoc, ‚Üê Int.gcd_assoc, Int.gcd_comm a b]

/-- CaseII. -/
theorem caseII {a b c : ‚Ñ§} {p : ‚Ñï} [hpri : Fact p.Prime] (hreg : IsRegularPrime p) (hodd : p ‚â† 2)
    (hprod : a * b * c ‚â† 0) (hgcd : ({a, b, c} : Finset ‚Ñ§).gcd id = 1)
    (caseII : ‚Üëp ‚à£ a * b * c) : a ^ p + b ^ p ‚â† c ^ p := by
  intro e
  simp only [ne_eq, mul_eq_zero, not_or] at hprod
  obtain ‚ü®‚ü®a0, b0‚ü©, c0‚ü© := hprod
  have hodd' := Nat.Prime.odd_of_ne_two hpri.out hodd
  obtain hab | hc := (Nat.prime_iff_prime_int.mp hpri.out).dvd_or_dvd caseII
  ¬∑ obtain ha| hb := (Nat.prime_iff_prime_int.mp hpri.out).dvd_or_dvd hab
    ¬∑ refine not_exists_Int_solution' hreg hodd ‚ü®b, -c, -a, ?_, ?_, ?_, ?_‚ü©
      ¬∑ simp only [‚Üê hgcd, Finset.mem_singleton, Finset.mem_insert, neg_inj, Finset.gcd_insert,
          id_eq, ‚Üê Int.coe_gcd, Int.neg_gcd, Nat.cast_inj, ‚Üê LawfulSingleton.insert_empty_eq,
            Finset.gcd_empty, Int.gcd_left_comm _ a]
      ¬∑ rwa [dvd_neg]
      ¬∑ rwa [ne_eq, neg_eq_zero]
      ¬∑ simp [hodd'.neg_pow, ‚Üê e]
    ¬∑ refine not_exists_Int_solution' hreg hodd ‚ü®-c, a, -b, ?_, ?_, ?_, ?_‚ü©
      ¬∑ simp only [‚Üê hgcd, Finset.mem_singleton, Finset.mem_insert, neg_inj, Finset.gcd_insert,
          id_eq, ‚Üê Int.coe_gcd, Int.neg_gcd, Nat.cast_inj, ‚Üê LawfulSingleton.insert_empty_eq,
          Finset.gcd_empty, Int.gcd_left_comm _ c]
      ¬∑ rwa [dvd_neg]
      ¬∑ rwa [ne_eq, neg_eq_zero]
      ¬∑ simp [hodd'.neg_pow, ‚Üê e]
  ¬∑ exact not_exists_Int_solution' hreg hodd ‚ü®a, b, c, hgcd, hc, c0, e‚ü©

end FltRegular
