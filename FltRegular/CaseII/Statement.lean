import FltRegular.CaseII.InductionStep

open scoped nonZeroDivisors NumberField
open Polynomial

variable {K : Type} {p : â„•} [hpri : Fact p.Prime] [Field K] [NumberField K]
  [IsCyclotomicExtension {p} â„š K] (hp : p â‰  2) [Fintype (ClassGroup (ğ“ K))]
  (hreg : p.Coprime <| Fintype.card <| ClassGroup (ğ“ K))

variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p)

namespace FltRegular

include hp hreg in
lemma not_exists_solution {m : â„•} (hm : 1 â‰¤ m) :
  Â¬âˆƒ (x' y' z' : ğ“ K) (Îµâ‚ƒ : (ğ“ K)Ë£),
    Â¬((hÎ¶.unit' : ğ“ K) - 1 âˆ£ y') âˆ§ Â¬((hÎ¶.unit' : ğ“ K) - 1 âˆ£ z') âˆ§
    x' ^ p + y' ^ p = Îµâ‚ƒ * (((hÎ¶.unit' : ğ“ K) - 1) ^ m * z') ^ p := by
  induction m, hm using Nat.le_induction with
  | base =>
      rintro âŸ¨x, y, z, Îµâ‚ƒ, hy, hz, eâŸ©
      exact zero_lt_one.not_ge (one_le_m hp hÎ¶ e hy hz)
  | succ m' _ IH =>
      rintro âŸ¨x, y, z, Îµâ‚ƒ, hy, hz, eâŸ©
      exact IH (exists_solution' hp hÎ¶ e hy hz hreg)

include hp hreg in
lemma not_exists_solution' :
  Â¬âˆƒ (x y z : ğ“ K), Â¬(hÎ¶.unit' : ğ“ K) - 1 âˆ£ y âˆ§ (hÎ¶.unit' : ğ“ K) - 1 âˆ£ z âˆ§ z â‰  0 âˆ§
    x ^ p + y ^ p = z ^ p := by
  letI : Fact (Nat.Prime p) := hpri
  letI : WfDvdMonoid (ğ“ K) := IsNoetherianRing.wfDvdMonoid
  rintro âŸ¨x, y, z, hy, hz, hz', eâŸ©
  obtain âŸ¨m, z, hm, hz'', rflâŸ© :
    âˆƒ m z', 1 â‰¤ m âˆ§ Â¬((hÎ¶.unit' : ğ“ K) - 1 âˆ£ z') âˆ§ z = ((hÎ¶.unit' : ğ“ K) - 1) ^ m * z' := by
    classical
    have H : FiniteMultiplicity ((hÎ¶.unit' : ğ“ K) - 1) z := FiniteMultiplicity.of_not_isUnit
      hÎ¶.zeta_sub_one_prime'.not_unit hz'
    obtain âŸ¨z', hâŸ© := pow_multiplicity_dvd ((hÎ¶.unit' : ğ“ K) - 1) z
    refine âŸ¨_, _, ?_, ?_, hâŸ©
    Â· rwa [â† Nat.cast_le (Î± := ENat), â† FiniteMultiplicity.emultiplicity_eq_multiplicity H,
        â† pow_dvd_iff_le_emultiplicity, pow_one]
    Â· intro h'
      have := mul_dvd_mul_left (((hÎ¶.unit' : ğ“ K) - 1) ^ (multiplicity ((hÎ¶.unit' : ğ“ K) - 1) z)) h'
      rw [â† pow_succ, â† h] at this
      refine not_pow_dvd_of_emultiplicity_lt ?_ this
      rw [FiniteMultiplicity.emultiplicity_eq_multiplicity H, Nat.cast_lt]
      exact Nat.lt_succ_self _
  refine not_exists_solution hp hreg hÎ¶ hm âŸ¨x, y, z, 1, hy, hz'', ?_âŸ©
  rwa [Units.val_one, one_mul]

lemma not_exists_Int_solution {p : â„•} [hpri : Fact (Nat.Prime p)] (hreg : IsRegularPrime p)
    (hodd : p â‰  2) : Â¬âˆƒ (x y z : â„¤), Â¬â†‘p âˆ£ y âˆ§ â†‘p âˆ£ z âˆ§ z â‰  0 âˆ§ x ^ p + y ^ p = z ^ p := by
  haveI := CyclotomicField.isCyclotomicExtension p â„š
  obtain âŸ¨Î¶, hÎ¶âŸ© := IsCyclotomicExtension.exists_isPrimitiveRoot
    â„š (B := (CyclotomicField p â„š)) (Set.mem_singleton p) hpri.1.ne_zero
  have := fun n â†¦ zeta_sub_one_dvd_Int_iff (K := CyclotomicField p â„š) hÎ¶ (n := n)
  simp_rw [â† this]
  rintro âŸ¨x, y, z, hy, hz, hz', eâŸ©
  refine not_exists_solution' (K := CyclotomicField p â„š) hodd ?_
    hÎ¶ âŸ¨x, y, z, hy, hz, ?_, ?_âŸ©
  Â· convert hreg
  Â· rwa [ne_eq, Int.cast_eq_zero]
  Â· dsimp
    simp_rw [â† Int.cast_pow, â† Int.cast_add, e]

lemma not_exists_Int_solution' {p : â„•} [hpri : Fact (Nat.Prime p)] (hreg : IsRegularPrime p)
    (hodd : p â‰  2) :
    Â¬âˆƒ (x y z : â„¤), ({x, y, z} : Finset â„¤).gcd id = 1 âˆ§ â†‘p âˆ£ z âˆ§ z â‰  0 âˆ§ x ^ p + y ^ p = z ^ p := by
  rintro âŸ¨x, y, z, hgcd, hz, hz', eâŸ©
  refine not_exists_Int_solution hreg hodd âŸ¨x, y, z, ?_, hz, hz', eâŸ©
  intro hy
  have := dvd_sub (dvd_pow hz hpri.out.ne_zero) (dvd_pow hy hpri.out.ne_zero)
  rw [â† e, add_sub_cancel_right] at this
  replace this := (Nat.prime_iff_prime_int.mp hpri.out).dvd_of_dvd_pow this
  apply (Nat.prime_iff_prime_int.mp hpri.out).not_unit
  rw [isUnit_iff_dvd_one, â† hgcd]
  simp [dvd_gcd_iff, hz, hy, this]

section Mathlib.Data.Int.Init

lemma Int.gcd_left_comm (a b c : â„¤) : Int.gcd a (Int.gcd b c) = Int.gcd b (Int.gcd a c) := by
  rw [â† Int.gcd_assoc, â† Int.gcd_assoc, Int.gcd_comm a b]

end Mathlib.Data.Int.Init

/-- CaseII. -/
theorem caseII {a b c : â„¤} {p : â„•} [hpri : Fact p.Prime] (hreg : IsRegularPrime p) (hodd : p â‰  2)
    (hprod : a * b * c â‰  0) (hgcd : ({a, b, c} : Finset â„¤).gcd id = 1)
    (caseII : â†‘p âˆ£ a * b * c) : a ^ p + b ^ p â‰  c ^ p := by
  intro e
  simp only [ne_eq, mul_eq_zero, not_or] at hprod
  obtain âŸ¨âŸ¨a0, b0âŸ©, c0âŸ© := hprod
  have hodd' := Nat.Prime.odd_of_ne_two hpri.out hodd
  obtain hab | hc := (Nat.prime_iff_prime_int.mp hpri.out).dvd_or_dvd caseII
  Â· obtain ha| hb := (Nat.prime_iff_prime_int.mp hpri.out).dvd_or_dvd hab
    Â· refine not_exists_Int_solution' hreg hodd âŸ¨b, -c, -a, ?_, ?_, ?_, ?_âŸ©
      Â· simp only [â† hgcd, Finset.gcd_insert,id_eq, â† Int.coe_gcd, Int.neg_gcd,
          â† LawfulSingleton.insert_empty_eq, Finset.gcd_empty, Int.gcd_left_comm _ a]
      Â· rwa [dvd_neg]
      Â· rwa [ne_eq, neg_eq_zero]
      Â· simp [hodd'.neg_pow, â† e]
    Â· refine not_exists_Int_solution' hreg hodd âŸ¨-c, a, -b, ?_, ?_, ?_, ?_âŸ©
      Â· simp only [â† hgcd, Finset.gcd_insert, id_eq, â† Int.coe_gcd, Int.neg_gcd,
          â† LawfulSingleton.insert_empty_eq, Finset.gcd_empty, Int.gcd_left_comm _ c]
      Â· rwa [dvd_neg]
      Â· rwa [ne_eq, neg_eq_zero]
      Â· simp [hodd'.neg_pow, â† e]
  Â· exact not_exists_Int_solution' hreg hodd âŸ¨a, b, c, hgcd, hc, c0, eâŸ©

end FltRegular
