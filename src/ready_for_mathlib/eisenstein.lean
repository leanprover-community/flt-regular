/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/

import ring_theory.eisenstein_criterion

/-!
# Eisenstein polynomials
Given an ideal `ğ“Ÿ` of a commutative semiring `R`, we say that a polynomial `f : polynomial R` is
*Eisenstein at `ğ“Ÿ`* if `f.leading_coeff âˆ‰ ğ“Ÿ`, `âˆ€ n, n < f.nat_degree â†’ f.coeff n âˆˆ ğ“Ÿ` and
`f.coeff 0 âˆ‰ ğ“Ÿ ^ 2`.

## Main definition
* `polynomial.is_eisenstein_at f ğ“Ÿ` : the property of being Eisenstein at `ğ“Ÿ`.

## Main results
* `polynomial.is_eisenstein_at.irreducible` : if a primitive `f` satisfies `f.is_eisenstein_at ğ“Ÿ`,
  where `ğ“Ÿ.is_prime`, then `f` is irreducible.
-/

universe u

variables {R : Type u}

namespace polynomial

/-- Given an ideal `ğ“Ÿ` of a commutative semiring `R`, we say that a polynomial `f : polynomial R`
is *Eisenstein at `ğ“Ÿ`* if `f.leading_coeff âˆ‰ ğ“Ÿ`, `âˆ€ n, n < f.nat_degree â†’ f.coeff n âˆˆ ğ“Ÿ` and
`f.coeff 0 âˆ‰ ğ“Ÿ ^ 2`. -/
@[mk_iff] structure is_eisenstein_at [comm_semiring R] (f : polynomial R) (ğ“Ÿ : ideal R) : Prop :=
(leading : f.leading_coeff âˆ‰ ğ“Ÿ)
(mem : âˆ€ {n}, n < f.nat_degree â†’ f.coeff n âˆˆ ğ“Ÿ)
(not_mem : f.coeff 0 âˆ‰ ğ“Ÿ ^ 2)

namespace is_eisenstein_at

lemma coeff_mem [comm_semiring R] {f : polynomial R} {ğ“Ÿ : ideal R} {n : â„•} (hn : n â‰  f.nat_degree)
  (hf : f.is_eisenstein_at ğ“Ÿ) : f.coeff n âˆˆ ğ“Ÿ :=
begin
  cases ne_iff_lt_or_gt.1 hn,
  { exact hf.mem h },
  { rw [coeff_eq_zero_of_nat_degree_lt h],
    exact ideal.zero_mem _}
end

/-- If a primitive `f` satisfies `f.is_eisenstein_at ğ“Ÿ`, where `ğ“Ÿ.is_prime`, then `f` is
irreducible. -/
lemma irreducible [comm_ring R] [is_domain R] {f : polynomial R} {ğ“Ÿ : ideal R}
  (hprime : ğ“Ÿ.is_prime) (hu : f.is_primitive) (hei : f.is_eisenstein_at ğ“Ÿ)
  (hfd0 : 0 < f.nat_degree) : irreducible f :=
irreducible_of_eisenstein_criterion hprime hei.leading (Î» n hn, hei.mem (coe_lt_degree.1 hn))
  (nat_degree_pos_iff_degree_pos.1 hfd0) hei.not_mem hu

end is_eisenstein_at

end polynomial
